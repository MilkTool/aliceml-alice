(* Alice Snake 2.0 - Snake abstract data type (impl)
 *
 *  Authors: Benedikt Grundmann / Sebastian Germesin
 *
 *  $Revision$
 *
 *  Last updated: $Date$ by $Author$
 * 
 *
 *)
import signature SNAKE      from "SNAKE-sig"
import structure Protocol   from "../common/Protocol"
import structure Pos        from "../common/Pos"
import structure Color      from "../common/Color"

structure Snake :> SNAKE =
struct
    structure P = Protocol

    
    datatype snake = SN of {
        direction : Protocol.direction ref,
        belly :     int ref,
        id :        int,
        color :     Color.color,
        body :      Pos.pos list ref
    }

    type t = snake
    
    
    fun snake (id, c, pos, d) = SN { 
            direction   = ref d, 
            belly       = ref 0, 
            color       = c,
            id          = id,
            body        = ref [pos]
        }

    fun color (SN { color = c, ... }) = c
    
    fun id    (SN { id = id, ... }) = id

    fun belly (SN { belly = ref b, ... })   = b

    fun eat (SN { belly, ... }, food)       = belly := !belly + food

    fun direction (SN { direction, ... })   = !direction

    fun move (SN {  direction = direction as ref old_dir, 
                    belly,
                    body = body_ref as ref body,
                    ...
                 }, new_dir) =
        let 
            (* determine new head position:
               if requested turn is impossible (ie.e new-dir 180° old-dir)
               then do not turn at all. 
             *)
            val head    = assert not (List.null body) do List.hd body

            val (new_head, dir) = 
               case (old_dir, new_dir) of
                    ((P.UP, P.DOWN) |    
                     (non P.DOWN, P.UP)) =>
                    (Pos.above head, P.UP)
                |   ((P.DOWN, P.UP) |
                     (non P.UP, P.DOWN)) =>
                    (Pos.below head, P.DOWN)
                |   ((P.LEFT, P.RIGHT) |
                     (non P.RIGHT, P.LEFT)) =>
                    (Pos.left head, P.LEFT)
                |   ((P.RIGHT, P.LEFT) |
                     (non P.LEFT, P.RIGHT)) =>
                    (Pos.right head, P.RIGHT)
        in
            body_ref := (if !belly > 0 then 
                            (belly := !belly - 1; new_head :: body)
                        else new_head :: (List.rev (List.tl (List.rev body))));
            direction := dir
        end

    fun head (SN { body = ref b, ... }) = assert not (List.null b) do List.hd b

    fun body (SN { body = ref b, ... }) = assert not (List.null b) do List.tl b 

    fun appBody f (SN { body = ref b, ... }) = 
        assert not (List.null b) do List.app f (List.tl b)

    fun length (SN { body = ref b, ... }) = List.length b

end
