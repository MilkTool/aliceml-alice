(*
 * Author:
 *   Leif Kornstaedt <kornstae@ps.uni-sb.de>
 *
 * Copyright:
 *   Leif Kornstaedt, 2002
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import structure Url             from "../lib/utility/Url"
import structure Signature       from "../lib/system/Signature"
import structure IO              from "../lib/system/IO"
import structure OS              from "../lib/system/OS"
import structure Reflect         from "../lib/system/Reflect"
import structure Resolver        from "../lib/system/Resolver"
import structure UnsafeComponent from "../lib/system/UnsafeComponent"
import signature MY_COMPONENT    from "MY_COMPONENT-sig"

structure MyComponent :> MY_COMPONENT =
struct
    type sign = Signature.t option
    type str = Reflect.value

    datatype component =
	UNEVALUATED of { imports: (string * sign) vector,
			 body:    str vector -> str,
			 sign:    sign }
      | EVALUATED of sign * str
    type t = component

    val localize =
	let
	    val handlers =
		case OS.Process.getEnv "ALICE_LOAD" of
		    SOME s => Resolver.Handler.parse s
		  | NONE => [Resolver.Handler.default]
	    val resolver = Resolver.new ("load", handlers)
	in
	    Resolver.localize resolver
	end

    fun load url =
	let
	    val s = Url.toStringRaw url   (*--** use getLocalPath instead *)
	in
	    case localize s of
		SOME (Resolver.FILE s') => UnsafeComponent.load s'
	      | SOME (Resolver.STRING s') => UnsafeComponent.unpack_ s'
	      | NONE =>
		    raise IO.Io {name = s,
				 function = "load",
				 cause = Option.Option}
	end

    val save = UnsafeComponent.save

    fun imports (UNEVALUATED {imports, ...}) = imports
      | imports (EVALUATED (_, _)) = #[]

    fun body (UNEVALUATED {body, ...}) = body
      | body (EVALUATED (_, str)) = fn #[] => str

    fun sign (UNEVALUATED {sign, ...}) = sign
      | sign (EVALUATED (sign, _)) = sign

    fun new {imports, body, sign} = UNEVALUATED {imports, body, sign}

    fun stripImportSigns (UNEVALUATED {imports, body, sign}) =
	let
	    val imports' = Vector.map (fn (s, _) => (s, NONE)) imports
	in
	    UNEVALUATED {imports = imports', body, sign}
	end
      | stripImportSigns (component as EVALUATED (_, _)) = component
end
