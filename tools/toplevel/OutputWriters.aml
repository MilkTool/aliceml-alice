import structure Stream from "x-alice:/lib/data/Stream"
import structure Gtk from "x-alice:/lib/gtk/Gtk"
import structure Gui from "Gui"
import structure Gdk from "x-alice:/lib/gtk/Gdk"
import structure Core from "x-alice:/lib/gtk/Core"
import structure Config from "Config"
import structure Evaluator from "Evaluator"
import structure BufferHighlight from "BufferHighlight"

structure OutputWriters =
   struct
      open Config

      (* Stream redirection *)

      fun markWriter (mark, TextPrimIO.WR ops) =
	  let
	      val mark' = Substring.full (String.str mark)
	      val writeVec = Option.valOf (#writeVec ops)
	      fun marked f sl = (writeVec mark'; f sl)
	  in
	      TextPrimIO.WR {ops where
			     writeVec = Option.map marked (#writeVec ops),
			     writeArr = Option.map marked (#writeArr ops),
			     writeVecNB = Option.map marked (#writeVecNB ops),
			     writeArrNB = Option.map marked (#writeArrNB ops)}
	  end

      val (stdin, stdInText) = Stream.stream ()
      val stdInReader = TextPrimIO.openVectorListRd stdInText
      val progStdIn = TextIO.StreamIO.mkInstream (stdInReader, "")
      val _ = TextIO.setInstream (TextIO.stdIn, progStdIn)

      val (stdText, stdWriter) = TextPrimIO.openVectorListWr ()

      val stdOutWriter = markWriter (#"\001", stdWriter)
      val progStdOut = TextIO.StreamIO.mkOutstream (stdOutWriter, IO.LINE_BUF)
      val _ = TextIO.setOutstream (TextIO.stdOut, progStdOut)

      val stdErrWriter = markWriter (#"\002", stdWriter)
      val progStdErr = TextIO.StreamIO.mkOutstream (stdErrWriter, IO.NO_BUF)
      val _ = TextIO.setOutstream (TextIO.stdErr, progStdErr)

      val (compilerText, compilerWriter) = TextPrimIO.openVectorListWr ()

      val compilerInWriter = markWriter (#"\000", compilerWriter)
      val compilerIn = TextIO.StreamIO.mkOutstream (compilerInWriter, IO.LINE_BUF)

      val compilerOutWriter = markWriter (#"\001", compilerWriter)
      val compilerOut = TextIO.StreamIO.mkOutstream (compilerOutWriter, IO.LINE_BUF)
      val _ = Evaluator.setOutStream compilerOut

      val compilerErrWriter = markWriter (#"\002", compilerWriter)
      val compilerErr = TextIO.StreamIO.mkOutstream (compilerErrWriter, IO.LINE_BUF)
      val _ = Evaluator.setErrStream compilerErr

      val (traceText, compilerTraceWriter) = TextPrimIO.openVectorListWr ()
      val compilerTrace = TextIO.StreamIO.mkOutstream (compilerTraceWriter, IO.LINE_BUF)
      val _ = Evaluator.setTraceStream compilerTrace

      val (rowCol, rowColText) = Stream.stream ()


      (* Highlight *)

      val highlight = BufferHighlight.highlight

      val stdFont = Gtk.pangoFontDescriptionFromString "Courier"

      fun fillBuf (view, buffer, handler, tags, highlight, text) =
          let
              val iter = Gtk.textIterNew()
              val _ = Gtk.textBufferGetEndIter(buffer,iter)
 	      val startOffset = Gtk.textIterGetOffset iter
	      val start = Gtk.textIterCopy iter
              val text = Core.latin1ToUtf8 text
          in
              Core.signalHandlerBlock(buffer, handler);
	      Gtk.textBufferInsert(buffer, iter, text, size text);
              Gtk.textBufferGetIterAtOffset (buffer, start, startOffset);
              app (fn t => Gtk.textBufferApplyTag(buffer, t, start, iter)) tags;
              highlight (buffer, start, iter, false);
              Gtk.textBufferPlaceCursor(buffer,iter);
              Gtk.textViewScrollToMark(view,
                                       Gtk.textBufferGetInsert buffer,
                                       0.0,false,0.0,0.0);
              Core.signalHandlerUnblock(buffer, handler)
          end
              
      val contextId = Gtk.statusbarGetContextId(Gui.statusbar, "Alice")
      val _ = Gtk.statusbarPush(Gui.statusbar,contextId, "Ready.");

      fun setStatus msg = 
          let
              val msg = if String.isPrefix "-- " msg then
                            String.extract(msg, 3, NONE)
                        else msg
              val msg = String.map (fn #"\n" => #" " | c => c) msg
          in
              (Gtk.statusbarPop(Gui.statusbar, contextId);
               Gtk.statusbarPush(Gui.statusbar, contextId, msg);
               ())
          end

      val rowColId = Gtk.statusbarGetContextId(Gui.statusbar1, "rowcol")
      val _ = Gtk.statusbarPush(Gui.statusbar1,rowColId,
                                "L 0, C 0");

      fun setRowCol (row, col) =
          let
              val r = Int.toString row
              val c = Int.toString col
              val msg = "L "^r^", C "^c
          in
              (Gtk.statusbarPop(Gui.statusbar1, rowColId);
               Gtk.statusbarPush(Gui.statusbar1, rowColId, msg);
               ())
              
          end

	val cmap   = Gdk.colormapGetSystem ()
        val textColor = Gdk.colorNew(0xf0f0, 0x2020, 0xa0a0)
	val black = Gdk.colorNew(0, 0, 0)
	val white = Gdk.colorNew(65535, 65535, 65535)
	val _ = Gdk.colormapAllocColor(cmap, black, false, true)
	val _ = Gdk.colormapAllocColor(cmap, white, false, true)
	val _ = Gdk.colormapAllocColor(cmap, textColor, false, true)

        fun progInInsert (stdInStream,noedit) (buf, [Gtk.INT off,
                                            Gtk.STRING txt,
                                            Gtk.INT size]) =
            if String.sub(txt, String.size txt - 1) = #"\n" then
                let
                    val enditer = Gtk.textIterNew()
                    val cursor = Gtk.textBufferGetInsert buf
                    val _ = Gtk.textBufferGetIterAtMark(buf,enditer,cursor)
                    val startiter = Gtk.textIterCopy enditer
                    val _ = Gtk.textIterBackwardToTagToggle(startiter,noedit)
                    val str = Gtk.textBufferGetText(buf, startiter, enditer, false)
                    val str = Core.utf8ToLatin1 str
                    val _ = Stream.send(stdInStream, str)
                in
                    Gtk.textBufferApplyTag(buf, noedit, startiter, enditer)
                end
            else ()
          | progInInsert _ _ = () (* Gtk is crazy *)

        fun lastChar' iter =
            if Gtk.textIterBackwardChar iter then
                let
                    val c = Gtk.textIterGetChar iter
                in
                    if c <= Char.maxOrd andalso Char.isSpace(chr c)
                    then lastChar' iter
                    else SOME c
                end
            else NONE
        fun lastChar(buf, off) =
            let
                val iter = Gtk.textIterNew()
            in
                Gtk.textBufferGetIterAtOffset(buf, iter, off);
                lastChar' iter
            end

        fun compilerInsert (eval,noedit) (buf, [Gtk.INT off,
                                            Gtk.STRING txt,
                                            Gtk.INT size]) =
            if String.sub(txt, String.size txt - 1) = #"\n" then
	       if lastChar(buf, off+size-1) = SOME (ord #";")
        	then
                    let
                	val enditer = Gtk.textIterNew()
                	val cursor = Gtk.textBufferGetInsert buf
                	val _ = Gtk.textBufferGetIterAtMark(buf,enditer,cursor)
                	val startiter = Gtk.textIterCopy enditer
                	val _ = Gtk.textIterBackwardToTagToggle(startiter, noedit)
                	val str = Gtk.textBufferGetText(buf, startiter, enditer, false)
                	val _ = highlight(buf, startiter, enditer, false)
                	val str = Core.utf8ToLatin1 str
                	val _ = eval str
                    in
                	Gtk.textBufferApplyTag(buf, noedit, startiter, enditer)
                    end
		else
		    Gtk.textBufferInsertAtCursor(buf, "  ", ~1)
            else ()
          | compilerInsert _ _ = () (* Gtk is crazy *)

      fun makeWriters eval =
          let
              val progBuf = Gtk.textBufferNew Gtk.NULL
              val compilerBuf = Gtk.textBufferNew Gtk.NULL
              val {err=pErr,log=pLog,plain=pStd,noedit=pNoEd,...} = makeTags progBuf
              val {err=cErr,log=cLog,plain=cStd,noedit=cNoEd,...} = makeTags compilerBuf
              val pSig = Gtk.signalConnect(progBuf, "insert-text",
                                           progInInsert (stdin, pNoEd))
              val cSig = Gtk.signalConnect(compilerBuf, "insert-text",
                                           compilerInsert (eval, cNoEd));

              fun forward (widget, buf, signal, noed, styles, highlights) =
		  let
		      fun receive (text, mark) =
			  if String.size text = 0 then mark else
			  if String.sub (text, 0) < #"\010"
			  then receive (String.extract (text, 1, NONE),
					Char.ord (String.sub (text, 0)))
			  else (fillBuf (widget, buf, signal,
					 [Vector.sub (styles, mark), noed],
					 Vector.sub (highlights, mark), text);
				mark)
		  in
		      receive
		  end
          in
              Gtk.widgetModifyFont(Gui.compilerout, stdFont);
              Gtk.textViewSetBuffer(Gui.progout, progBuf);
              Gtk.textViewSetBuffer(Gui.compilerout, compilerBuf);
	      spawn foldl (forward (Gui.compilerout, compilerBuf, cSig, cNoEd,
				    #[cStd, cLog, cErr],
				    #[highlight, highlight, const ()])) 1 compilerText;
	      spawn foldl (forward (Gui.progout, progBuf, pSig, pNoEd,
				    #[pStd, pStd, pErr],
				    #[const (), const (), const ()])) 1 stdText;
              spawn app setStatus traceText;
              spawn app setRowCol rowColText
          end
   end
