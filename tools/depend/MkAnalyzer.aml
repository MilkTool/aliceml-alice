(*
 * Author:
 *   Leif Kornstaedt <kornstae@ps.uni-sb.de>
 *
 * Contributor:
 *   Andreas Rossberg <rossberg@ps.uni-sb.de>
 *
 * Copyright:
 *   Leif Kornstaedt, 2001
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import structure TextIO   from "../lib/system/TextIO"
import structure Config   from "../lib/system/Config"
import structure Url      from "../lib/utility/Url"
import signature ANALYZER from "ANALYZER-sig"


functor MkAnalyzer(structure Parser: PARSER
		   val extension: string): ANALYZER =
    struct
	fun warn message =
	    TextIO.output (TextIO.stdErr, "warning: " ^ message ^ "\n")

	local
	    val homeref: string option ref = ref NONE
	in
	    fun home () =
		case !homeref of
		    NONE =>
			let
			    val home =
				case Config.homeDir of
				    SOME s => s ^ "/"
				  | NONE =>
					(warn ("could not determine " ^
					       "installation directory");
					 "")
			in
			    homeref := SOME home; home
			end
		  | SOME home => home
	end

	fun parseUrl url =
	    case (Url.getScheme url, Url.getAuthority url) of
		(NONE, NONE) =>
		    SOME(Url.toString url)
	      | (SOME "file", NONE) =>
		    SOME(Url.toString (Url.setScheme (url, NONE)))
	      | _ => NONE

	fun pathFloor filename =
	    let
		val fro = "." ^ extension
		val n = String.size filename
		val m = String.size fro
	    in
		if n > m andalso String.substring (filename, n - m, m) = fro
		then String.substring (filename, 0, n - m)
		else filename
	    end

	fun analyzeImport (desc, url) =
	    let
		val url =
		    case desc of
			SOME base => Url.resolve base url
		      | NONE => url
	    in
		case parseUrl url of
		    SOME targetFilename =>
			SOME (pathFloor targetFilename ^ "." ^ extension)
		  | NONE =>
			(TextIO.output(TextIO.stdErr,
				       "warning: ignoring URI " ^
				       Url.toString url ^ "\n");
			 NONE)
	    end

	fun analyze (desc, source) =
	    List.mapPartial (fn s => analyzeImport (desc, Url.fromString s))
	    (Parser.parse source)

	fun readFile filename =
	    let
		val file   = TextIO.openIn filename
		val source = TextIO.inputAll file
		val _      = TextIO.closeIn file
	    in
		source
	    end

	fun analyzeString source = analyze (NONE, source)

	fun analyzeFile filename =
	    let
		val url = Url.fromString filename
	    in
		analyze (SOME url, readFile (Option.valOf (parseUrl url)))
		handle (Option.Option | IO.Io _) =>
		    (TextIO.output(TextIO.stdErr,
				   "unable to access file " ^ filename ^ "\n");
		     nil)
	    end
    end
