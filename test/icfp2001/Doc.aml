import structure Tag from "Tag"

signature DOC =
sig
    datatype item =
	  TEXT   of string list	(* reversed *)
	| TAGGED of Tag.tag * doc
    withtype doc = item list

    val output : TextIO.outstream * doc -> unit
end

structure Doc : DOC =
struct
    datatype item =
	  TEXT   of string list	(* reversed *)
	| TAGGED of Tag.tag * doc
    withtype doc = item list

    fun output(out, items) = List.app (fn x => output'(out, x)) items
    and output'(out, TEXT ss) =
	    List.appr (fn s => TextIO.output(out, s)) ss
      | output'(out, TAGGED(t,doc)) =
	let
	    val tag = Tag.toString t
	in
	    TextIO.output(out, "<" ^ tag ^ ">");
	    output(out, doc);
	    TextIO.output(out, "</" ^ tag ^ ">")
	end
end
