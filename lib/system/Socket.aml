(*
 * Author:
 *   Leif Kornstaedt <kornstae@ps.uni-sb.de>
 *
 * Copyright:
 *   Leif Kornstaedt, 2001-2002
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import structure UnsafeSocket from "UnsafeSocket"
import structure IO           from "IO"
import signature SOCKET       from "SOCKET-sig"

structure Socket :> SOCKET =
    struct
	type socket
	type t = socket
	type vector = string
	type elem = char

	type host = string
	type port = int

	fun server (portOpt, acceptFun) =
	    let
		val serverPort = Option.getOpt (portOpt, 0)
		val (serverSocket, serverPort) = UnsafeSocket.server serverPort
		fun acceptLoop () =
		    let
			val (clientSocket, clientHost, clientPort) =
			    UnsafeSocket.accept serverSocket
		    in
			acceptFun (clientSocket, clientHost, clientPort);
			acceptLoop ()
		    end
	    in
		spawn acceptLoop ();
		(serverSocket, serverPort)
	    end

	fun client (host, port) =
	    UnsafeSocket.client (host, port)
	    handle UnsafeSocket.Io {name, function, cause} =>
		raise IO.Io {name, function, cause}

	fun input1 socket =
	    UnsafeSocket.input1 socket
	    handle UnsafeSocket.Io {name, function, cause} =>
		raise IO.Io {name, function, cause}

	fun inputN (socket, n) = String.concat (inputN' (socket, n))
	and inputN' (socket, n) =
	    let
		val s = UnsafeSocket.inputN (socket, n)
			handle UnsafeSocket.Io {name, function, cause} =>
			    raise IO.Io {name, function, cause}
		val i = String.size s
	    in
		if i = 0 orelse i = n then [s]
		else s::inputN' (socket, n - i)
	    end

	fun inputLine socket =
	    case input1 socket of
		 SOME c => SOME (String.implode (c::inputLine' socket))
	       | NONE => NONE
	and inputLine' socket =
	    case input1 socket of
		SOME #"\n" => [#"\n"]
	      | SOME c => c::inputLine' socket
	      | NONE => [#"\n"]

	fun output1 (socket, c) =
	    UnsafeSocket.output1 (socket, c)
	    handle UnsafeSocket.Io {name, function, cause} =>
		raise IO.Io {name, function, cause}

	fun output (socket, s) =
	    output' (socket, s, 0)
	    handle UnsafeSocket.Io {name, function, cause} =>
		raise IO.Io {name, function, cause}
	and output' (socket, s, i) =
	    if i = String.size s then ()
	    else output' (socket, s, i + UnsafeSocket.output (socket, s, i))

	val close = UnsafeSocket.close
    end
