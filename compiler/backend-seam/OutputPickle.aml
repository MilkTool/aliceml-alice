(*
 * Author:
 *   Leif Kornstaedt <kornstae@ps.uni-sb.de>
 *
 * Copyright:
 *   Leif Kornstaedt, 2000-2002
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import structure Reflect             from "../../lib/system/Reflect"
import structure Crash               from "../infrastructure/Crash"
import structure Stamp               from "../common/Stamp"
import structure StampMap            from "../common/StampMap"
import structure PrimPickle          from "PrimPickle"
import structure Value               from "ValueCross"
import structure AbstractCodeGrammar from "AbstractCodeGrammar"
import signature OUTPUT_PICKLE       from "OUTPUT_PICKLE-sig"

structure OutputPickle :> OUTPUT_PICKLE =
    struct
	structure V = Value
	open AbstractCodeGrammar

	structure Label =
	    struct
		datatype t =
		    ARRAY
		  | CELL
		  | CON_VAL
		  | RECORD
		  | VECTOR
		  | TAG of LargeInt.int

		val none = 0: LargeInt.int
		val some = TAG 1

		val idDef    = TAG 0
		val wildcard = 1: LargeInt.int

		val oneArg  = TAG 0
		val tupArgs = TAG 1

		val appPrim        = TAG 0
		val appVar         = TAG 1
		val close          = TAG 2
		val compactIntTest = TAG 3
		val compactTagTest = TAG 4
		val conTest        = TAG 5
		val directAppVar   = TAG 6
		val endHandle      = TAG 7
		val endTry         = TAG 8
		val getRef         = TAG 9
		val getTup         = TAG 10
		val intTest        = TAG 11
		val kill           = TAG 12
		val lazyPolySel    = TAG 13
		val putCon         = TAG 14
		val putNew         = TAG 15
		val putPolyRec     = TAG 16
		val putRef         = TAG 17
		val putTag         = TAG 18
		val putTup         = TAG 19
		val putVar         = TAG 20
		val putVec         = TAG 21
		val raise_         = TAG 22
		val realTest       = TAG 23
		val reraise        = TAG 24
		val return         = TAG 25
		val sel            = TAG 26
		val shared         = TAG 27
		val specialize     = TAG 28
		val stringTest     = TAG 29
		val tagTest        = TAG 30
		val try            = TAG 31
		val vecTest        = TAG 32

		val global       = TAG 0
		val immediate    = TAG 1
		val lastUseLocal = TAG 2
		val local_       = TAG 3
		val toplevel     = TAG 4

		val template = TAG 0

		val function    = TAG 0
		val specialized = TAG 1

		fun toInt ARRAY   = 0
		  | toInt CELL    = 1
		  | toInt CON_VAL = 2
		  | toInt RECORD  = 3
		  | toInt VECTOR  = 4
		  | toInt (TAG i) = 5 + i
	    end

	type context = {outstream: PrimPickle.outstream,
			shared: PrimPickle.id StampMap.t}

	fun outputInt ({outstream, ...}: context, i) =
	    PrimPickle.outputInt (outstream, i)
	fun outputChunk ({outstream, ...}: context, words) =
	    PrimPickle.outputChunk (outstream, words)
	fun outputUnique ({outstream, ...}: context) =
	    PrimPickle.outputUnique outstream
	fun outputBlock ({outstream, ...}: context, label, size) =
	    PrimPickle.outputBlock (outstream, Label.toInt label, size)
	fun outputTuple ({outstream, ...}: context, size) =
	    PrimPickle.outputTuple (outstream, size)
	fun outputClosure ({outstream, ...}: context, size) =
	    PrimPickle.outputClosure (outstream, size)
	fun outputReference ({outstream, ...}: context, id) =
	    PrimPickle.outputReference (outstream, id)
	fun outputString ({outstream, ...}: context, s) =
	    PrimPickle.outputString (outstream, s)
	fun outputUniqueString ({outstream, ...}: context, s) =
	    PrimPickle.outputUniqueString (outstream, s)
	fun outputTransform ({outstream, ...}: context, name) =
	    PrimPickle.outputTransform (outstream, name)

	fun outputOption _ (context, NONE) =
	    outputInt (context, Label.none)
	  | outputOption output (context, SOME x) =
	    (outputBlock (context, Label.some, 1); output (context, x))

	fun outputVector output (context, xs) =
	    (outputBlock (context, Label.VECTOR, Vector.length xs + 1);
	     outputInt (context, LargeInt.fromInt (Vector.length xs));
	     Vector.app (fn x => output (context, x)) xs)

	fun outputStamp (context, stamp) =
	    outputInt (context, LargeInt.fromInt (Stamp.hash stamp))

	fun outputId (context, id) = outputInt (context, LargeInt.fromInt id)

	fun outputIdDef (context, IdDef id) =
	    (outputBlock (context, Label.idDef, 1); outputId (context, id))
	  | outputIdDef (context, Wildcard) =
	    outputInt (context, Label.wildcard)

	fun outputArgs outputArg (context, OneArg x) =
	    (outputBlock (context, Label.oneArg, 1); outputArg (context, x))
	  | outputArgs outputArg (context, TupArgs xs) =
	    (outputBlock (context, Label.tupArgs, 1);
	     outputVector outputArg (context, xs))

	fun outputCoord (context, (string, line, column)) =
	    (outputTuple (context, 3); outputString (context, string);
	     outputInt (context, LargeInt.fromInt line);
	     outputInt (context, LargeInt.fromInt column))

	fun outputLiveness (context, is) =
	    outputVector (fn (context, i) =>
			     outputInt (context, LargeInt.fromInt i))
			 (context, is)

	fun outputWideString (context, s) =
	    outputChunk (context,
			 Vector.fromList
			 (List.foldr (fn (c, cs) =>
				      let
					  val i = Char.ord c
				      in
					  Word8.fromInt (i div 0x100)::
					  Word8.fromInt (i mod 0x100)::cs
				      end) nil (String.explode s)))

	fun outputReal (context, r) =
	    let
		val vec = Reflect.realToVector r
	    in
		outputChunk (context,
			     Vector.tabulate
				 (8, fn i => Word8Vector.sub (vec, i)));
		()
	    end

	fun outputLabel (context, label) =
	    ignore (outputUniqueString (context, label))

	fun outputName (context, string) =
	    ignore (outputString (context, string))

	fun outputValue (context, V.Prim name) =
	    (outputTransform (context, "Alice.primitive.value");
	     ignore (outputString (context, name)))
	  | outputValue (context, V.Int i) = outputInt (context, i)
	  | outputValue (context, V.String s) =
	    ignore (outputString (context, s))
	  | outputValue (context, V.WideString s) =
	    ignore (outputWideString (context, s))
	  | outputValue (context, V.Real r) = outputReal (context, r)
	  | outputValue (context, V.TaggedValue (i, values)) =
	    (outputBlock (context, Label.TAG i, Vector.length values);
	     Vector.app (fn value => outputValue (context, value)) values)
	  | outputValue (context, V.Tuple values) =
	    (outputTuple (context, Vector.length values);
	     Vector.app (fn value => outputValue (context, value)) values)
	  | outputValue (context, V.Vector values) =
	    (outputBlock (context, Label.VECTOR, Vector.length values + 1);
	     outputInt (context, LargeInt.fromInt (Vector.length values));
	     Vector.app (fn value => outputValue (context, value)) values)
	  | outputValue (context, V.Closure (abstractCode, values)) = 
	    (outputClosure (context, 1 + Vector.length values);
	     outputTransform (context, "Alice.function");
	     outputAbstractCode (context, abstractCode);
	     Vector.app (fn value => outputValue (context, value)) values)
	  | outputValue (context, V.Sign _) =
	    outputInt (context, 0) (*--** i.e., `NONE' *)
	and outputInstr (context, Kill (ids, instr)) =
	    (outputBlock (context, Label.kill, 2);
	     outputVector outputId (context, ids);
	     outputInstr (context, instr))
	  | outputInstr (context, PutVar (id, idRef, instr)) =
	    (outputBlock (context, Label.putVar, 3); outputId (context, id);
	     outputIdRef (context, idRef); outputInstr (context, instr))
	  | outputInstr (context, PutNew (id, s, instr)) =
	    (outputBlock (context, Label.putNew, 3); outputId (context, id);
	     outputString (context, s); outputInstr (context, instr))
	  | outputInstr (context, PutTag (id, tag, idRefs, instr)) =
	    (outputBlock (context, Label.putTag, 4); outputId (context, id);
	     outputInt (context, LargeInt.fromInt tag);
	     outputVector outputIdRef (context, idRefs);
	     outputInstr (context, instr))
	  | outputInstr (context, PutCon (id, idRef, idRefs, instr)) =
	    (outputBlock (context, Label.putCon, 4); outputId (context, id);
	     outputIdRef (context, idRef);
	     outputVector outputIdRef (context, idRefs);
	     outputInstr (context, instr))
	  | outputInstr (context, PutRef (id, idRef, instr)) =
	    (outputBlock (context, Label.putRef, 3); outputId (context, id);
	     outputIdRef (context, idRef); outputInstr (context, instr))
	  | outputInstr (context, PutTup (id, idRefs, instr)) =
	    (outputBlock (context, Label.putTup, 3); outputId (context, id);
	     outputVector outputIdRef (context, idRefs);
	     outputInstr (context, instr))
	  | outputInstr (context, PutPolyRec (id, labels, idRefs, instr)) =
	    (outputBlock (context, Label.putPolyRec, 4);
	     outputId (context, id);
	     outputVector outputLabel (context, labels);
	     outputVector outputIdRef (context, idRefs);
	     outputInstr (context, instr))
	  | outputInstr (context, PutVec (id, idRefs, instr)) =
	    (outputBlock (context, Label.putVec, 3); outputId (context, id);
	     outputVector outputIdRef (context, idRefs);
	     outputInstr (context, instr))
	  | outputInstr (context, Close (id, idRefs, template, instr)) =
	    (outputBlock (context, Label.close, 4); outputId (context, id);
	     outputVector outputIdRef (context, idRefs);
	     outputTemplate (context, template); outputInstr (context, instr))
	  | outputInstr (context, Specialize (id, idRefs, template, instr)) =
	    (outputBlock (context, Label.specialize, 4);
	     outputId (context, id);
	     outputVector outputIdRef (context, idRefs);
	     outputTemplate (context, template); outputInstr (context, instr))
	  | outputInstr (context, AppPrim (value, idRefs, idDefInstrOpt)) =
	    (outputBlock (context, Label.appPrim, 3);
	     outputValue (context, value);
	     outputVector outputIdRef (context, idRefs);
	     outputOption (fn (context, (idDef, instr)) =>
			   (outputTuple (context, 2);
			    outputIdDef (context, idDef);
			    outputInstr (context, instr)))
	     (context, idDefInstrOpt))
	  | outputInstr (context, AppVar (idRef, inArgs, outArgsInstrOpt)) =
	    (outputBlock (context, Label.appVar, 3);
	     outputIdRef (context, idRef);
	     outputArgs outputIdRef (context, inArgs);
	     outputOption (fn (context, (outArgs, instr)) =>
			   (outputTuple (context, 2);
			    outputArgs outputIdDef (context, outArgs);
			    outputInstr (context, instr)))
	     (context, outArgsInstrOpt))
	  | outputInstr (context,
			 DirectAppVar (idRef, inArgs, outArgsInstrOpt)) =
	    (outputBlock (context, Label.directAppVar, 3);
	     outputIdRef (context, idRef);
	     outputArgs outputIdRef (context, inArgs);
	     outputOption (fn (context, (outArgs, instr)) =>
			   (outputTuple (context, 2);
			    outputArgs outputIdDef (context, outArgs);
			    outputInstr (context, instr)))
	     (context, outArgsInstrOpt))
	  | outputInstr (context, GetRef (id, idRef, instr)) =
	    (outputBlock (context, Label.getRef, 3); outputId (context, id);
	     outputIdRef (context, idRef); outputInstr (context, instr))
	  | outputInstr (context, GetTup  (idDefs, idRef, instr)) =
	    (outputBlock (context, Label.getTup, 3);
	     outputVector outputIdDef (context, idDefs);
	     outputIdRef (context, idRef); outputInstr (context, instr))
	  | outputInstr (context, Sel (id, idRef, i, instr)) =
	    (outputBlock (context, Label.sel, 4);
	     outputId (context, id); outputIdRef (context, idRef);
	     outputInt (context, LargeInt.fromInt i);
	     outputInstr (context, instr))
	  | outputInstr (context, LazyPolySel (ids, idRef, labels, instr)) =
	    (outputBlock (context, Label.lazyPolySel, 4);
	     outputVector outputId (context, ids);
	     outputIdRef (context, idRef);
	     outputVector outputLabel (context, labels);
	     outputInstr (context, instr))
	  | outputInstr (context, Raise idRef) =
	    (outputBlock (context, Label.raise_, 1);
	     outputIdRef (context, idRef))
	  | outputInstr (context, Reraise idRef) =
	    (outputBlock (context, Label.reraise, 1);
	     outputIdRef (context, idRef))
	  | outputInstr (context,
			 Try (tryInstr, idDef1, idDef2, handleInstr)) =
	    (outputBlock (context, Label.try, 4);
	     outputInstr (context, tryInstr); outputIdDef (context, idDef1);
	     outputIdDef (context, idDef2); outputInstr (context, handleInstr))
	  | outputInstr (context, EndTry instr) =
	    (outputBlock (context, Label.endTry, 1);
	     outputInstr (context, instr))
	  | outputInstr (context, EndHandle instr) =
	    (outputBlock (context, Label.endHandle, 1);
	     outputInstr (context, instr))
	  | outputInstr (context, IntTest (idRef, tests, instr)) =
	    (outputBlock (context, Label.intTest, 3);
	     outputIdRef (context, idRef);
	     outputVector (fn (context, (int, instr)) =>
			      (outputTuple (context, 2);
			       outputInt (context, int);
			       outputInstr (context, instr))) (context, tests);
	     outputInstr (context, instr))
	  | outputInstr (context, CompactIntTest (idRef, i, instrs, instr)) =
	    (outputBlock (context, Label.compactIntTest, 4);
	     outputIdRef (context, idRef); outputInt (context, i);
	     outputVector outputInstr (context, instrs);
	     outputInstr (context, instr))
	  | outputInstr (context, RealTest (idRef, tests, instr)) =
	    (outputBlock (context, Label.realTest, 3);
	     outputIdRef (context, idRef);
	     outputVector (fn (context, (real, instr)) =>
			   (outputTuple (context, 2);
			    outputReal (context, real);
			    outputInstr (context, instr))) (context, tests);
	     outputInstr (context, instr))
	  | outputInstr (context, StringTest (idRef, tests, instr)) =
	    (outputBlock (context, Label.stringTest, 3);
	     outputIdRef (context, idRef);
	     outputVector (fn (context, (string, instr)) =>
			   (outputTuple (context, 2);
			    outputString (context, string);
			    outputInstr (context, instr))) (context, tests);
	     outputInstr (context, instr))
	  | outputInstr (context, TagTest (idRef, tests1, tests2, instr)) =
	    (outputBlock (context, Label.tagTest, 4);
	     outputIdRef (context, idRef);
	     outputVector (fn (context, (tag, instr)) =>
			      (outputTuple (context, 2);
			       outputInt (context, LargeInt.fromInt tag);
			       outputInstr (context, instr)))
			  (context, tests1);
	     outputVector (fn (context, (tag, idDefs, instr)) =>
			      (outputTuple (context, 3);
			       outputInt (context, LargeInt.fromInt tag);
			       outputVector outputIdDef (context, idDefs);
			       outputInstr (context, instr)))
			  (context, tests2);
	     outputInstr (context, instr))
	  | outputInstr (context, CompactTagTest (idRef, tests, instr)) =
	    (outputBlock (context, Label.compactTagTest, 3);
	     outputIdRef (context, idRef);
	     outputVector (fn (context, (idDefsOpt, instr)) =>
			      (outputTuple (context, 2);
			       outputOption
				   (fn (context, idDefs) =>
				       outputVector
					   outputIdDef (context, idDefs))
				   (context, idDefsOpt);
			       outputInstr (context, instr))) (context, tests);
	     outputInstr (context, instr))
	  | outputInstr (context, ConTest (idRef, tests1, tests2, instr)) =
	    (outputBlock (context, Label.conTest, 4);
	     outputIdRef (context, idRef);
	     outputVector (fn (context, (idRef, instr)) =>
			   (outputTuple (context, 2);
			    outputIdRef (context, idRef);
			    outputInstr (context, instr))) (context, tests1);
	     outputVector (fn (context, (idRef, idDefs, instr)) =>
			   (outputTuple (context, 3);
			    outputIdRef (context, idRef);
			    outputVector outputIdDef (context, idDefs);
			    outputInstr (context, instr))) (context, tests2);
	     outputInstr (context, instr))
	  | outputInstr (context, VecTest (idRef, tests, instr)) =
	    (outputBlock (context, Label.vecTest, 3);
	     outputIdRef (context, idRef);
	     outputVector (fn (context, (idDefs, instr)) =>
			   (outputTuple (context, 2);
			    outputVector outputIdDef (context, idDefs);
			    outputInstr (context, instr))) (context, tests);
	     outputInstr (context, instr))
	  | outputInstr (context as {shared, ...}, Shared (stamp, instr)) =
	    (case StampMap.lookup (shared, stamp) of
		 SOME id => outputReference (context, id)
	       | NONE =>
		     let
			 val id = outputBlock (context, Label.shared, 2)
		     in
			 outputStamp (context, stamp);
			 outputInstr (context, instr);
			 StampMap.insert (shared, stamp, id)
		     end)
	  | outputInstr (context, Return args) =
	    (outputBlock (context, Label.return, 1);
	     outputArgs outputIdRef (context, args))
	and outputIdRef (context, Immediate value) =
	    (outputBlock (context, Label.immediate, 1);
	     outputValue (context, value))
	  | outputIdRef (context, Local id) =
	    (outputBlock (context, Label.local_, 1); outputId (context, id))
	  | outputIdRef (context, LastUseLocal id) =
	    (outputBlock (context, Label.lastUseLocal, 1);
	     outputId (context, id))
	  | outputIdRef (context, Global i) =
	    (outputBlock (context, Label.global, 1); outputId (context, i))
	and outputTemplate (context, Template (coord, ntoplevels, localNames,
					       args, body, liveness)) =
	    (outputBlock (context, Label.template, 6);
	     outputCoord (context, coord);
	     outputInt (context, LargeInt.fromInt ntoplevels);
	     outputVector outputName (context, localNames);
	     outputArgs outputIdDef (context, args);
	     outputInstr (context, body); outputLiveness (context, liveness))
	and outputAbstractCode (context, Function (coord, globals, localNames,
						   args, body, liveness)) =
	    (outputBlock (context, Label.function, 6);
	     outputCoord (context, coord);
	     outputVector (outputOption outputValue) (context, globals);
	     outputVector outputName (context, localNames);
	     outputArgs outputIdDef (context, args);
	     outputInstr (context, body); outputLiveness (context, liveness))

	fun output (outstream, value) =
	    outputValue ({outstream = outstream, shared = StampMap.new ()},
			 value)
    end
