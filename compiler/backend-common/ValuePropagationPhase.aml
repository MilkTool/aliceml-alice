(*
 * Author:
 *   Leif Kornstaedt <kornstae@ps.uni-sb.de>
 *
 * Copyright:
 *   Leif Kornstaedt, 2000-2002
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

(*
 * This file implements value propagation (a variant on constant propagation
 * and constant folding).  For each program point, an approximation of the
 * environment that it will see at run-time is computed, and if possible,
 * the computations are simplified to take advantage of these minimal
 * assumptions.
 *
 * This analysis is - for now - not interprocedural.
 *)

import structure TextIO (*--**DEBUG*)    from "../../lib/system/TextIO"
import structure PrettyPrint             from "../../lib/utility/PrettyPrint"
import structure Assert                  from "../../lib/utility/Assert"
import signature HASH_KEY                from "../../lib/utility/HASH_KEY-sig"
import signature IMP_MAP                 from "../../lib/utility/IMP_MAP-sig"
import structure MkHashImpMap            from "../../lib/utility/MkHashImpMap"
import structure MkDepthFirstSearch from "../../lib/utility/MkDepthFirstSearch"
import structure Label                   from "../../lib/rtt/Label"
import structure Crash                   from "../infrastructure/Crash"
import structure Source                  from "../infrastructure/Source"
import structure Error                   from "../infrastructure/Error"
import signature CONTEXT                 from "../infrastructure/CONTEXT-sig"
import structure Stamp                   from "../common/Stamp"
import structure StampSet                from "../common/StampSet"
import structure StampMap                from "../common/StampMap"
import structure Arity                   from "Arity"
import structure FlatGrammar             from "FlatGrammar"
import structure OutputFlatGrammar       from "OutputFlatGrammar"
import structure PrimOps                 from "PrimOps"
import signature VALUE_PROPAGATION_PHASE from "VALUE_PROPAGATION_PHASE-sig"

(*
 * We need to implement our own maps because the ones provided by
 * the library are not fit for our purposes:  The iterators yield
 * all elements of the map, regardless of their scope and whether
 * they are shadowed; this would introduce a severe performance bug.
 *)

signature SCOPED_IMP_MAP =
    sig
	type key
	type 'a map
	type 'a t = 'a map

	exception Lookup of key

	val new: unit -> 'a map
	val clone: 'a map -> 'a map
	val cloneTop: 'a map -> 'a map
	val insertScope: 'a map -> unit
	val deleteScope: 'a map -> unit
	val insert: 'a map * key * 'a -> unit
	val insertDeep: 'a map * key * 'a -> unit   (* Lookup *)
	val lookup: 'a map * key -> 'a option
	val lookupExistent: 'a map * key -> 'a            (* Lookup *)
	val appiScope: (key * 'a -> unit) -> 'a map -> unit
	val foldi: (key * 'a * 'b -> 'b) -> 'b -> 'a map -> 'b
    end

functor MkScopedImpMap(ImpMap: IMP_MAP) :>
    SCOPED_IMP_MAP where type key = ImpMap.key =
    struct
	type key = ImpMap.key
	type 'a map = 'a ImpMap.t list ref
	type 'a t = 'a map

	exception Lookup = ImpMap.Lookup

	fun new () = ref [ImpMap.new ()]

	fun clone (ref maps) = ref (List.map ImpMap.clone maps)

	fun cloneTop (ref maps) =
	    ref (ImpMap.clone (List.hd maps)::List.tl maps)

	fun insertScope r = r := ImpMap.new ()::(!r)
	fun deleteScope r = r := List.tl (!r)

	fun insert (ref maps, key, entry) =
	    ImpMap.insert (List.hd maps, key, entry)

	fun insertDeep (ref maps, key, entry) =
	    insertDeep' (maps, key, entry)
	and insertDeep' ([map], key, entry) =
	    ImpMap.insert (map, key, entry)
	  | insertDeep' (_::maps, key, entry) = insertDeep' (maps, key, entry)
	  | insertDeep' (nil, _, _) = raise Empty

	fun lookup (ref maps, key) = lookup' (maps, key)
	and lookup' (nil, _) = NONE
	  | lookup' ([map], key) = ImpMap.lookup (map, key)
	  | lookup' (map::rest, key) =
	    case ImpMap.lookup (map, key) of
		NONE => lookup' (rest, key)
	      | res as SOME _ => res

	fun lookupExistent (mapsRef, key) =
	    case lookup (mapsRef, key) of
		SOME entry => entry
	      | NONE => raise Lookup key

	fun appiScope f (ref maps) = ImpMap.appi f (List.hd maps)

	fun foldi f z (ref maps) = foldi' (maps, f, z)
	and foldi' (map::maps, f, z) =
	    foldi' (maps, f, ImpMap.foldi f z map)
	  | foldi' (nil, _, z) = z
    end

functor MkHashScopedImpMap(Key: HASH_KEY) =
    MkScopedImpMap(MkHashImpMap(Key))

structure ValuePropagationPhase :> VALUE_PROPAGATION_PHASE =
    struct
	structure I = FlatGrammar
	structure O = FlatGrammar

	open I

	fun idEq (Id (_, stamp1, _), Id (_, stamp2, _)) = stamp1 = stamp2

	structure IdMap =
	    MkHashScopedImpMap(type t = id
			       val equal = idEq
			       fun hash (Id (_, stamp, _)) =
				   Stamp.hash stamp)
	structure LabelMap = MkHashImpMap(Label)

	(*
	 * The `value' data type represents the approximation of the value
	 * a given identifier will have at a given program point.  Only the
	 * values which can actually be used to simplify computations are
	 * stored.
	 *)

	datatype value =
	    LitVal of lit
	  | PrimVal of string
	  | VarVal of id
	  | TagVal of label * int * id option args
	  | ConVal of id * id option args
	  | ProdVal of id option vector
	  | PolyProdVal of id option LabelMap.t
	  | VecVal of id option vector
	  | FunVal of stamp * idDef args * body
	  | CaughtExnVal of id
	  | UnknownVal
	  | FailedVal

	fun mapArgs f (OneArg x) = OneArg (f x)
	  | mapArgs f (TupArgs xs) = TupArgs (Vector.map f xs)
	  | mapArgs f (ProdArgs labelXVec) =
	    ProdArgs (Vector.map (fn (label, x) => (label, f x)) labelXVec)

	fun allArgs f (OneArg x) = f x
	  | allArgs f (TupArgs xs) = Vector.all f xs
	  | allArgs f (ProdArgs labelXVec) =
	    Vector.all (fn (_, x) => f x) labelXVec

	fun idRefId (IdRef id) = id
	  | idRefId (LastIdRef id) = id
	  | idRefId (Lit _ | Prim _) =
	    raise Crash.Crash "ValuePropagationPhase.idRefId"

	fun idRefIdOpt (IdRef id | LastIdRef id) = SOME id
	  | idRefIdOpt (Lit _ | Prim _) = NONE

	fun idDefIdOpt (IdDef id) = SOME id
	  | idDefIdOpt Wildcard = NONE

	fun expToValue (NewExp (_, _)) = UnknownVal   (*--** ConVal? *)
	  | expToValue (VarExp (_, Lit lit)) = LitVal lit
	  | expToValue (VarExp (_, Prim name)) = PrimVal name
	  | expToValue (VarExp (_, (IdRef id | LastIdRef id))) = VarVal id
	  | expToValue (TagExp (_, label, n, args)) =
	    TagVal (label, n, mapArgs idRefIdOpt args)
	  | expToValue (ConExp (_, (IdRef id | LastIdRef id), args)) =
	    ConVal (id, mapArgs idRefIdOpt args)
	  | expToValue (ConExp (_, (Lit _ | Prim _), _)) = UnknownVal
	  | expToValue (RefExp (_, _)) = UnknownVal
	  | expToValue (TupExp (_, idRefs)) =
	    ProdVal (Vector.map idRefIdOpt idRefs)
	  | expToValue (ProdExp (_, labelIdRefVec)) =
	    ProdVal (Vector.map (fn (_, idRef) => idRefIdOpt idRef)
				labelIdRefVec)
	  | expToValue (PolyProdExp (_, labelIdRefVec)) =
	    let
		val map = LabelMap.new ()
	    in
		Vector.app (fn (label, idRef) =>
			       LabelMap.insertDisjoint
				   (map, label, idRefIdOpt idRef))
			   labelIdRefVec;
		PolyProdVal map
	    end
	  | expToValue (VecExp (_, idRefs)) =
	    VecVal (Vector.map idRefIdOpt idRefs)
	  | expToValue (FunExp (_, stamp, _, args, body)) =
	    FunVal (stamp, args, body)
	  | expToValue (PrimAppExp (_, _, _)) = UnknownVal
	  | expToValue (VarAppExp (_, _, _)) = UnknownVal
	  | expToValue (SelExp (_, _, _, _, _)) = UnknownVal
	  | expToValue (LazyPolySelExp (_, _, _)) = UnknownVal
	  | expToValue (FunAppExp (_, _, _, _)) = UnknownVal
	  | expToValue (FailExp _) = FailedVal

	(*
	 * For each identifier, we store in the environment whether it is
	 * declared on the top level of the current component (i.e., outside
	 * any function) and what approximation we can give about its value.
	 *)

	type isToplevel = bool
	type env = (value * isToplevel) IdMap.t

	structure C: CONTEXT =
	    struct
		type t = env

		val new = IdMap.new
		val clone = IdMap.clone
	    end

	(*
	 * Value propagation performed on a given sequence of statements
	 * is only correct if we give it a value environment that we can
	 * minimally assume to hold at run-time at this program point.
	 * For control-flow junctions, this means we have to merge all
	 * incoming environments in order to build an environment of
	 * correct assumptions for the remainder of the control path.
	 *
	 * The control-flow junctions in our language are the SharedStm
	 * nodes.  In order to have the correct environment when performing
	 * value propagation on a SharedStm, therefore, we need to have
	 * analyzed all paths leading to this SharedStm.  In general this
	 * means that we have to build an abstract control-flow graph
	 * containing only the SharedStms as nodes and having edges from
	 * a node n to a node m iff there is a control-flow branch leading
	 * from n to m.  If then we analyze SharedStms in an order conforming
	 * to the topological ordering of the graph's nodes, the minimal
	 * environments (as defined above) are correctly computed.
	 *
	 * sortShared computes an appropriate ordering on the SharedStms.
	 *)

	datatype 'a sharedEntry =
	    UNIQUE
	  | SHARED
	  | VISITED of 'a

	local
	    fun node (edgeMap, stamp) =
		StampMap.insertDisjoint (edgeMap, stamp, nil)

	    fun edge (edgeMap, pred, succ) =
		let
		    val stamps = StampMap.lookupExistent (edgeMap, pred)
		in
		    StampMap.insert (edgeMap, pred, succ::stamps)
		end

	    fun sortStm (LastUse (_, _), _, _, _, _) = ()
	      | sortStm (ValDec (_, _, _), _, _, _, _) = ()
	      | sortStm (RefDec (_, _, _), _, _, _, _) = ()
	      | sortStm (TupDec (_, _, _), _, _, _, _) = ()
	      | sortStm (ProdDec (_, _, _), _, _, _, _) = ()
	      | sortStm (RaiseStm (_, _), _, _, _, _) = ()
	      | sortStm (ReraiseStm (_, _), _, _, _, _) = ()
	      | sortStm (TryStm (_, tryBody, _, _, handleBody),
			 pred, edgeMap, shared, path) =
		(sortBody (tryBody, pred, edgeMap, shared, path);
		 sortBody (handleBody, pred, edgeMap, shared, path))
	      | sortStm (EndTryStm (_, body), pred, edgeMap, shared, path) =
		sortBody (body, pred, edgeMap, shared, path)
	      | sortStm (EndHandleStm (_, body), pred, edgeMap, shared, path) =
		sortBody (body, pred, edgeMap, shared, path)
	      | sortStm (TestStm (_, _, tests, body),
			 pred, edgeMap, shared, path) =
		(sortTests (tests, pred, edgeMap, shared, path);
		 sortBody (body, pred, edgeMap, shared, path))
	      | sortStm (SharedStm (_, body, stamp),
			 pred, edgeMap, shared, path) =
		(StampMap.insert (shared, stamp,
				  if StampMap.member (shared, stamp)
				  then SHARED else UNIQUE);
		 edge (edgeMap, pred, stamp);
		 sortSharedBody (body, stamp, edgeMap, shared, path))
	      | sortStm (ReturnStm (_, _), _, _, _, _) = ()
	      | sortStm (IndirectStm (_, ref bodyOpt),
			 pred, edgeMap, shared, path) =
		sortBody (valOf bodyOpt, pred, edgeMap, shared, path)
	      | sortStm (ExportStm (_, _), _, _, _, _) = ()
	    and sortTests (LitTests litBodyVec, pred, edgeMap, shared, path) =
		Vector.app (fn (_, body) =>
			    sortBody (body, pred, edgeMap, shared, path))
		litBodyVec
	      | sortTests (TagTests tagBodyVec, pred, edgeMap, shared, path) =
		Vector.app (fn (_, _, _, body) =>
			    sortBody (body, pred, edgeMap, shared, path))
		tagBodyVec
	      | sortTests (ConTests conBodyVec, pred, edgeMap, shared, path) =
		Vector.app (fn (_, _, body) =>
			    sortBody (body, pred, edgeMap, shared, path))
		conBodyVec
	      | sortTests (VecTests vecBodyVec, pred, edgeMap, shared, path) =
		Vector.app (fn (_, body) =>
			    sortBody (body, pred, edgeMap, shared, path))
		vecBodyVec
	    and sortBody (stm::stms, pred, edgeMap, shared, path) =
		(sortStm (stm, pred, edgeMap, shared, path);
		 sortBody (stms, pred, edgeMap, shared, path))
	      | sortBody (nil, _, _, _, _) = ()
	    and sortSharedBody (body, stamp, edgeMap, shared, path) =
		if StampMap.member (edgeMap, stamp) then ()
		else
		    (node (edgeMap, stamp);
		     StampSet.insertDisjoint (path, stamp);
		     sortBody (body, stamp, edgeMap, shared, path);
		     StampSet.deleteExistent (path, stamp))

	    structure DepthFirstSearch =
		MkDepthFirstSearch(structure Key = Stamp
				   structure Map = StampMap)
	in
	    fun sortShared (body, stamp) =
		let
		    val edgeMap = StampMap.new ()
		    val shared = StampMap.new ()
		    val path = StampSet.new ()
		in
		    sortSharedBody (body, stamp, edgeMap, shared, path);
		    (DepthFirstSearch.search edgeMap, shared)
		end
	end

	(*
	 * All lazy selections are hoisted to just after the declaration
	 * of the variable being selected.  This is performed by a
	 * post-processing patching step.
	 *)

	fun patchIdDef (Wildcard, _) = nil
	  | patchIdDef (IdDef id, lazySels) =
	    case IdMap.lookup (lazySels, id) of
		SOME map =>
		    LabelMap.foldi
			(fn (label, id', rest) =>
			    ValDec (Source.nowhere, IdDef id',
				    LazyPolySelExp (Source.nowhere,
						    label, IdRef id))::
			    patchIdDef (IdDef id', lazySels) @ rest) nil map
	      | NONE => nil

	fun patchIdDefs (idDefs, lazySels) =
	    Vector.foldr (fn (idDef, rest) =>
			     patchIdDef (idDef, lazySels) @ rest) nil idDefs

	fun patchArgs (OneArg idDef, lazySels) =
	    patchIdDef (idDef, lazySels)
	  | patchArgs (TupArgs idDefs, lazySels) =
	    patchIdDefs (idDefs, lazySels)
	  | patchArgs (ProdArgs labelIdDefVec, lazySels) =
	    Vector.foldr (fn ((_, idDef), rest) =>
			     patchIdDef (idDef, lazySels) @ rest) nil
			 labelIdDefVec

	fun patchBody ((stm as ValDec (_, idDef, _))::stms, lazySels, shared) =
	    stm::patchIdDef (idDef, lazySels) @
	    patchBody (stms, lazySels, shared)
	  | patchBody ((stm as RefDec (_, idDef, _))::stms, lazySels, shared) =
	    stm::patchIdDef (idDef, lazySels) @
	    patchBody (stms, lazySels, shared)
	  | patchBody ((stm as TupDec (_, idDefs, _))::stms,
		       lazySels, shared) =
	    stm::patchIdDefs (idDefs, lazySels) @
	    patchBody (stms, lazySels, shared)
	  | patchBody ((stm as ProdDec (_, labelIdDefVec, _))::stms,
		       lazySels, shared) =
	    stm::patchIdDefs (Vector.map #2 labelIdDefVec, lazySels) @
	    patchBody (stms, lazySels, shared)
	  | patchBody ([TryStm (info, tryBody, idDef1, idDef2, handleBody)],
		       lazySels, shared) =
	    [TryStm (info, patchBody (tryBody, lazySels, shared),
		     idDef1, idDef2,
		     patchIdDef (idDef1, lazySels) @
		     patchIdDef (idDef2, lazySels) @
		     patchBody (handleBody, lazySels, shared))]
	  | patchBody ([EndTryStm (info, body)], lazySels, shared) =
	    [EndTryStm (info, patchBody (body, lazySels, shared))]
	  | patchBody ([EndHandleStm (info, body)], lazySels, shared) =
	    [EndHandleStm (info, patchBody (body, lazySels, shared))]
	  | patchBody ([TestStm (info, id, tests, body)], lazySels, shared) =
	    [TestStm (info, id, patchTests (tests, lazySels, shared),
		      patchBody (body, lazySels, shared))]
	  | patchBody ([SharedStm (info, body, stamp)], lazySels, shared) =
	    (case StampMap.lookup (shared, stamp) of
		 SOME body => body
	       | NONE =>
		     let
			 val body = patchBody (body, lazySels, shared)
			 val body' = [SharedStm (info, body, stamp)]
		     in
			 StampMap.insert (shared, stamp, body');
			 body'
		     end)
	  | patchBody ([IndirectStm (_, ref (SOME body))], lazySels, shared) =
	    patchBody (body, lazySels, shared)
	  | patchBody (body as [(RaiseStm (_, _) | ReraiseStm (_, _) |
				 ReturnStm (_, _) | ExportStm (_, _))], _, _) =
	    body
	  | patchBody (_, _, _) =
	    raise Crash.Crash "ValuePropagationPhase.patchBody"
	and patchTests (LitTests tests, lazySels, shared) =
	    LitTests (Vector.map (fn (lit, body) =>
				  (lit, patchBody (body, lazySels, shared)))
		      tests)
	  | patchTests (TagTests tests, lazySels, shared) =
	    TagTests (Vector.map (fn (label, n, args, body) =>
				  (label, n, args,
				   patchArgs (args, lazySels) @
				   patchBody (body, lazySels, shared))) tests)
	  | patchTests (ConTests tests, lazySels, shared) =
	    ConTests (Vector.map (fn (id, args, body) =>
				  (id, args,
				   patchArgs (args, lazySels) @
				   patchBody (body, lazySels, shared))) tests)
	  | patchTests (VecTests tests, lazySels, shared) =
	    VecTests (Vector.map (fn (idDefs, body) =>
				  (idDefs,
				   patchIdDefs (idDefs, lazySels) @
				   patchBody (body, lazySels, shared))) tests)

	(*
	 * As outlined above, we need to compute the value environments we
	 * can minimally assume to hold at every given program point.  For
	 * control-flow junctions, this means that we have to combine the
	 * environments from all branches.
	 *
	 * `unionEnv' takes two environments and returns the combined minimal
	 * environment.  It is reflexive and associative, so an arbitrary
	 * number of environments can be combined using `unionEnv.'
	 *)

	fun idOptEq (SOME (Id (_, stamp, _)), SOME (Id (_, stamp', _))) =
	    stamp = stamp'
	  | idOptEq (NONE, NONE) = true
	  | idOptEq (_, _) = false

	fun argsMin (args as OneArg idOpt, OneArg idOpt') =
	    if idOptEq (idOpt, idOpt') then args else OneArg NONE
	  | argsMin (TupArgs idOpts, TupArgs idOpts') =
	    TupArgs (VectorPair.map (fn (idOpt, idOpt') =>
				     if idOptEq (idOpt, idOpt') then idOpt
				     else NONE) (idOpts, idOpts'))
	  | argsMin (ProdArgs labelIdOptVec, ProdArgs labelIdOptVec') =
	    ProdArgs (VectorPair.map (fn ((label, idOpt), (_, idOpt')) =>
				      (label,
				       if idOptEq (idOpt, idOpt') then idOpt
				       else NONE))
		      (labelIdOptVec, labelIdOptVec'))
	  | argsMin (_, _) = raise Crash.Crash "ValuePropagationPhase.argsMin"

	fun valueMin (value as LitVal lit, LitVal lit') =
	    if litEq (lit, lit') then value else UnknownVal
	  | valueMin (value as PrimVal name, PrimVal name') =
	    if name = name' then value else UnknownVal
	  | valueMin (value as VarVal (Id (_, stamp, _)),
		      VarVal (Id (_, stamp', _))) =
	    if stamp = stamp' then value else UnknownVal
	  | valueMin (TagVal (label, n, args), TagVal (_, n', args')) =
	    if n = n' then TagVal (label, n, argsMin (args, args'))
	    else UnknownVal
	  | valueMin (ConVal (id, args), ConVal (id', args')) =
	    if idEq (id, id') then ConVal (id, argsMin (args, args'))
	    else UnknownVal
	  | valueMin (ProdVal idOpts, ProdVal idOpts') =
	    ProdVal (VectorPair.map (fn (idOpt, idOpt') =>
				     if idOptEq (idOpt, idOpt')
				     then idOpt else NONE)
		     (idOpts, idOpts'))
	  | valueMin (value as PolyProdVal map, PolyProdVal map') =
	    if map = map' then value else UnknownVal
	  | valueMin (VecVal idOpts, VecVal idOpts') =
	    if Vector.length idOpts = Vector.length idOpts' then
		VecVal (VectorPair.map (fn (idOpt, idOpt') =>
					if idOptEq (idOpt, idOpt') then idOpt
					else NONE) (idOpts, idOpts'))
	    else UnknownVal
	  | valueMin (value as FunVal (stamp, _, _), FunVal (stamp', _, _)) =
	    if stamp = stamp' then value else UnknownVal
	  | valueMin (value as CaughtExnVal id, CaughtExnVal id') =
	    if idEq (id, id') then value else UnknownVal
	  | valueMin (_, _) = UnknownVal

	fun unionEnv (env', env) =
	    IdMap.appiScope
	    (fn (id, entry as (value, isToplevel)) =>
	     case IdMap.lookup (env', id) of
		 SOME (value', isToplevel') =>
		     let
			 val entry' = (valueMin (value, value'), isToplevel)
		     in
			 Assert.assert (isToplevel = isToplevel');
			 IdMap.insert (env', id, entry')
		     end
	       | NONE => IdMap.insert (env', id, entry)) env

	(* Various Helper Functions *)

	fun idRefEq ((IdRef id1 | LastIdRef id1),
		     (IdRef id2 | LastIdRef id2)) = idEq (id1, id2)
	  | idRefEq ((Lit _ | Prim _), _) = false
	  | idRefEq (_, (Lit _ | Prim _)) = false

	fun lookupEnv (env, (IdRef id | LastIdRef id)) =
	    IdMap.lookupExistent (env, id)
	  | lookupEnv (_, Lit lit) = (LitVal lit, true)
	  | lookupEnv (_, Prim name) = (PrimVal name, true)

	fun varExp (info, idRef, env) =
	    case lookupEnv (env, idRef) of
		(LitVal lit, _) => VarExp (info, Lit lit)
	      | (PrimVal name, _) => VarExp (info, Prim name)
	      | (VarVal id', _) => varExp (info, IdRef id', env)
	      | (TagVal (label, n, TupArgs #[]), _) =>
		    TagExp (info, label, n, TupArgs #[])
	      | (ConVal (id', TupArgs #[]), _) =>
		    ConExp (info, IdRef id', TupArgs #[])
	      | (_, _) => VarExp (info, idRef)

	fun deref (idRef, env) =
	    case lookupEnv (env, idRef) of
		(LitVal lit, _) => Lit lit
	      | (PrimVal name, _) => Prim name
	      | (VarVal id', _) => IdRef id'
	      | (_, _) => idRef

	fun derefArgs (OneArg idRef, env) = OneArg (deref (idRef, env))
	  | derefArgs (TupArgs idRefs, env) =
	    TupArgs (Vector.map (fn idRef => deref (idRef, env)) idRefs)
	  | derefArgs (ProdArgs labelIdRefVec, env) =
	    ProdArgs (Vector.map (fn (label, idRef) =>
				     (label, deref (idRef, env)))
				 labelIdRefVec)

	fun argsToIdRefs (OneArg idRef) = #[idRef]
	  | argsToIdRefs (TupArgs idRefs) = idRefs
	  | argsToIdRefs (ProdArgs labelIdRefVec) = Vector.map #2 labelIdRefVec

	fun doSel (n, idRef, env) =
	    case lookupEnv (env, idRef) of
		(ProdVal idOpts, _) =>
		    Option.map IdRef (Vector.sub (idOpts, n))
	      | (_, _) => NONE

	fun doLazySel (id, label, map, env, isToplevel) =
	    case LabelMap.lookup (map, label) of
		SOME id => IdRef id
	      | NONE =>
		    let
			val newId = freshId Source.nowhere
		    in
			IdMap.insertDeep (env, newId,
					  (UnknownVal, isToplevel));
			LabelMap.insert (map, label, newId);
			IdRef newId
		    end

	(* Calling Convention Conversion *)

	fun getIdRefs (idRef, env) =
	    case lookupEnv (env, idRef) of
		(ProdVal idOpts, _) =>
		    if Vector.all isSome idOpts then
			SOME (Vector.map valOf idOpts)
		    else NONE
	      | (_, _) => NONE

	fun argsToArity (OneArg _) = Arity.Unary
	  | argsToArity (TupArgs ids) = Arity.Tuple (Vector.length ids)
	  | argsToArity (ProdArgs labelIdVec) =
	    Arity.Product (Vector.map #1 labelIdVec)

	fun adaptArity (_, Arity.Unary, args as OneArg _, _, f) = (nil, f args)
	  | adaptArity (info, Arity.Unary, TupArgs idRefs, _, f) =
	    (* construct tuple *)
	    let
		val id = freshId info
	    in
		([ValDec (info, IdDef id, TupExp (info, idRefs))],
		 f (OneArg (IdRef id)))
	    end
	  | adaptArity (info, Arity.Unary, ProdArgs labelIdRefVec, _, f) =
	    (* construct product *)
	    let
		val id = freshId info
	    in
		([ValDec (info, IdDef id,
			  ProdExp (info, labelIdRefVec))],
		 f (OneArg (IdRef id)))
	    end
	  | adaptArity (info, Arity.Tuple n, OneArg idRef, env, f) =
	    (* deconstruct tuple (try statically) *)
	    (case getIdRefs (idRef, env) of
		 SOME ids => (nil, f (TupArgs (Vector.map IdRef ids)))
	       | NONE =>
		     let
			 val ids = Vector.tabulate (n, fn _ => freshId info)
		     in
			 ([TupDec (info, Vector.map IdDef ids, idRef)],
			  f (TupArgs (Vector.map IdRef ids)))
		     end)
	  | adaptArity (_, Arity.Tuple _, args as TupArgs _, _, f) =
	    (nil, f args)
	  | adaptArity (info, Arity.Product labels, OneArg idRef, env, f) =
	    (* deconstruct product (try statically) *)
	    (case getIdRefs (idRef, env) of
		 SOME ids =>
		     (nil, f (ProdArgs (VectorPair.zip
					    (labels, Vector.map IdRef ids))))
	       | NONE =>
		     let
			 val labelIdVec =
			     Vector.map (fn label => (label, freshId info))
					labels
			 val labelIdDefVec =
			     Vector.map (fn (label, id) => (label, IdDef id))
					labelIdVec
			 val labelIdRefVec =
			     Vector.map (fn (label, id) => (label, IdRef id))
					labelIdVec
		     in
			 ([ProdDec (info, labelIdDefVec, idRef)],
			  f (ProdArgs labelIdRefVec))
		     end)
	  | adaptArity (_, Arity.Product _, args as ProdArgs _, _, f) =
	    (nil, f args)
	  | adaptArity (_, _, _, _, _) =
	    raise Crash.Crash "ValuePropagationPhase.adaptArity"

	(* Applications of Primitives *)

	fun isNonlocal ((IdRef id | LastIdRef id), env) =
	    isSome (IdMap.lookup (env, id))
	  | isNonlocal ((Lit _ | Prim _), _) = true

	fun isSimple (NewExp (_, _), _) = true
	  | isSimple (VarExp (_, idRef), env) = isNonlocal (idRef, env)
	  | isSimple (TagExp (_, _, _, args), env) =
	    allArgs (fn idRef => isNonlocal (idRef, env)) args
	  | isSimple (ConExp (_, _, TupArgs #[]), _) = true
	  | isSimple (RefExp (_, idRef), env) = isNonlocal (idRef, env)
	  | isSimple (TupExp (_, idRefs), env) =
	    Vector.all (fn idRef => isNonlocal (idRef, env)) idRefs
	  | isSimple (ProdExp (_, labelIdRefVec), env) =
	    Vector.all (fn (_, idRef) => isNonlocal (idRef, env)) labelIdRefVec
	  | isSimple (PolyProdExp (_, labelIdRefVec), env) =
	    Vector.all (fn (_, idRef) => isNonlocal (idRef, env)) labelIdRefVec
	  | isSimple (VecExp (_, idRefs), env) =
	    Vector.all (fn idRef => isNonlocal (idRef, env)) idRefs
	  | isSimple (_, _) = false

	fun vpPrimApp (info, "Future.concur", idRefs as #[idRef], env) =
	    (case lookupEnv (env, idRef) of
		 (FunVal (_, _, [ReturnStm (_, exp)]), _) =>
		     if isSimple (exp, env) then exp
		     else PrimAppExp (info, "Future.concur", idRefs)
	       | (_, _) => PrimAppExp (info, "Future.concur", idRefs))
	  | vpPrimApp (info, name, idRefs, _) =
	    (*--** assertion about arity; evaluate application partially? *)
	    PrimAppExp (info, name, idRefs)

	(* Environment Handling *)

	fun alias (IdDef id, SOME id', env, isToplevel) =
	    IdMap.insert (env, id, (VarVal id', isToplevel))
	  | alias (_, _, _, _) = ()

	fun aliasArgs (OneArg idDef, OneArg idOpt', env, isToplevel) =
	    alias (idDef, idOpt', env, isToplevel)
	  | aliasArgs (TupArgs idDefs, TupArgs idOpts', env, isToplevel) =
	    VectorPair.app (fn (idDef, idOpt') =>
			       alias (idDef, idOpt', env, isToplevel))
			   (idDefs, idOpts')
	  | aliasArgs (ProdArgs labelIdDefVec, ProdArgs labelIdOptVec',
		       env, isToplevel) =
	    VectorPair.app (fn ((_, idDef), (_, idOpt')) =>
			       alias (idDef, idOpt', env, isToplevel))
			   (labelIdDefVec, labelIdOptVec')
	  | aliasArgs (_, _, _, _) =
	    raise Crash.Crash "ValuePropagationPhase.aliasArgs"

	fun declare (env, IdDef id, entry) = IdMap.insert (env, id, entry)
	  | declare (_, Wildcard, _) = ()

	fun declareUnknown (env, idDef, isToplevel) =
	    declare (env, idDef, (UnknownVal, isToplevel))

	fun declareArgs (env, OneArg idDef, isToplevel) =
	    declareUnknown (env, idDef, isToplevel)
	  | declareArgs (env, TupArgs idDefs, isToplevel) =
	    Vector.app (fn idDef =>
			declareUnknown (env, idDef, isToplevel)) idDefs
	  | declareArgs (env, ProdArgs labelIdDefVec, isToplevel) =
	    Vector.app (fn (_, idDef) =>
			declareUnknown (env, idDef, isToplevel)) labelIdDefVec

	(* The Main Recursion over the Syntax Tree *)

	fun testsAppend (NONE, testsOpt) = testsOpt
	  | testsAppend (testsOpt, NONE) = testsOpt
	  | testsAppend (SOME (LitTests xs), SOME (LitTests ys)) =
	    SOME (LitTests (Vector.concat [xs, ys]))
	  | testsAppend (SOME (TagTests xs), SOME (TagTests ys)) =
	    SOME (TagTests (Vector.concat [xs, ys]))
	  | testsAppend (SOME (ConTests xs), SOME (ConTests ys)) =
	    SOME (ConTests (Vector.concat [xs, ys]))
	  | testsAppend (SOME (VecTests xs), SOME (VecTests ys)) =
	    SOME (VecTests (Vector.concat [xs, ys]))
	  | testsAppend (SOME _, SOME _) =
	    raise Crash.Crash "ValuePropagationPhase.testsAppend"

	fun testsNull (SOME (LitTests xs)) = Vector.length xs = 0
	  | testsNull (SOME (TagTests xs)) = Vector.length xs = 0
	  | testsNull (SOME (ConTests xs)) = Vector.length xs = 0
	  | testsNull (SOME (VecTests xs)) = Vector.length xs = 0
	  | testsNull NONE = true

	fun vpBody (ValDec (info, idDef, exp)::stms,
		    state as {env, isToplevel, lazySels, ...}) =
	    let
		val (stms', exp) = vpExp (exp, env, isToplevel, lazySels)
	    in
		declare (env, idDef, (expToValue exp, isToplevel));
		stms' @ ValDec (info, idDef, exp)::vpBody (stms, state)
	    end
	  | vpBody (RefDec (info, idDef, idRef)::stms,
		    state as {env, isToplevel, ...}) =
	    (declareUnknown (env, idDef, isToplevel);
	     RefDec (info, idDef, deref (idRef, env))::vpBody (stms, state))
	  | vpBody (TupDec (info, idDefs, idRef)::stms,
		    state as {env, isToplevel, ...}) =
	    let
		val idRef = deref (idRef, env)
		val id = idRefId idRef
		val (idDefs, isDet) =
		    case IdMap.lookupExistent (env, id) of
			(ProdVal idOpts', isToplevel') =>
			    (VectorPair.map
				 (fn (IdDef id, SOME id') =>
		(* id and id' can be equal due to pattern matching compilation,
		 * for example in:
		 *    datatype t = A | B | C
		 *    fun f (a,b) (A, x) = x
		 *      | f (a,b) (B, x) = x
		 *      | f a_b (_, x) = x          <- here
		 *)
				     (if idEq (id, id') then ()
				      else IdMap.insert (env, id,
							 (VarVal id',
							  isToplevel'));
				      Wildcard)
				   | (idDef, _) => idDef)
				 (idDefs, idOpts'), true)
		      | (_, _) => (idDefs, false)
		val isSome = ref false
		val idOpts =
		    Vector.map (fn idDef =>
				   (declareUnknown (env, idDef, isToplevel);
				    case idDef of
					IdDef id => (isSome := true; SOME id)
				      | Wildcard => NONE)) idDefs
	    in
		IdMap.insert (env, id, (ProdVal idOpts, isToplevel));
		if !isSome orelse not isDet then
		    TupDec (info, idDefs, idRef)::vpBody (stms, state)
		else vpBody (stms, state)
	    end
	  | vpBody (ProdDec (info, labelIdDefVec, idRef)::stms,
		    state as {env, isToplevel, ...}) =
	    let
		val idRef = deref (idRef, env)
		val id = idRefId idRef
		val (labelIdDefVec, isDet) =
		    case IdMap.lookupExistent (env, id) of
			(ProdVal idOpts, isToplevel') =>
			    (VectorPair.map
				 (fn ((label, IdDef id), SOME id') =>
				     (if idEq (id, id') then ()
				      else IdMap.insert (env, id,
							 (VarVal id',
							  isToplevel'));
				      (label, Wildcard))
				   | (labelIdDef, _) => labelIdDef)
				 (labelIdDefVec, idOpts), true)
		      | (_, _) => (labelIdDefVec, false)
		val isSome = ref false
		val idOpts =
		    Vector.map (fn (_, idDef) =>
				   (declareUnknown (env, idDef, isToplevel);
				    case idDef of
					IdDef id => (isSome := true; SOME id)
				      | Wildcard => NONE)) labelIdDefVec
	    in
		IdMap.insert (env, id, (ProdVal idOpts, isToplevel));
		if !isSome orelse not isDet then
		    ProdDec (info, labelIdDefVec, idRef)::vpBody (stms, state)
		else vpBody (stms, state)
	    end
	  | vpBody ([stm as RaiseStm (info, idRef)], {env, ...}) =
	    let
		val idRef = deref (idRef, env)
	    in
		[case lookupEnv (env, idRef) of
		     (CaughtExnVal id', _) => ReraiseStm (info, IdRef id')
		   | _ => RaiseStm (info, idRef)]
	    end
	  | vpBody ([stm as ReraiseStm (info, idRef)], {env, ...}) =
	    [ReraiseStm (info, deref (idRef, env))]
	  | vpBody ([TryStm (info, tryBody, idDef1, idDef2, handleBody)],
		    state as {env, isToplevel, ...}) =
	    let
		val tryBody = vpBodyScope (tryBody, state)
		val _ =
		    declare (env, idDef2,
			     (case idDef1 of
				  IdDef id => CaughtExnVal id
				| _ => UnknownVal, isToplevel))
		val handleBody = vpBodyScope (handleBody, state)
	    in
		[TryStm (info, tryBody, idDef1, idDef2, handleBody)]
	    end
	  | vpBody ([EndTryStm (info, body)], state) =
	    [EndTryStm (info, vpBodyScope (body, state))]
	  | vpBody ([EndHandleStm (info, body)], state) =
	    [EndHandleStm (info, vpBodyScope (body, state))]
	  | vpBody ([stm as TestStm (info, idRef, _, _)],
		    state as {env, ...}) =
	    let
		val idRef = deref (idRef, env)
		val (testsOpt, elseBody) = vpTestStm ([stm], idRef, state)
	    in
		if testsNull testsOpt then elseBody
		else [TestStm (info, idRef, valOf testsOpt, elseBody)]
	    end
	  | vpBody ([SharedStm (info, body, stamp)],
		    state as {env, shared, ...}) =
	    (* We need to return a SharedStm with an IndirectStm in it;
	     * the IndirectStm's reference will be updated destructively
	     * later when analyses proceeds (topological ordering). *)
	    (case StampMap.lookupExistent (shared, stamp) of
		 UNIQUE => vpBody (body, state)
	       | SHARED =>
		     let
			 val bodyOptRef = ref (SOME body)
			 val entry =
			     VISITED (bodyOptRef, IdMap.cloneTop env)
		     in
			 StampMap.insert (shared, stamp, entry);
			 [SharedStm (info, [IndirectStm (info, bodyOptRef)],
				     stamp)]
		     end
	       | VISITED (bodyOptRef, env') =>
		     (unionEnv (env', env);
		      [SharedStm (info, [IndirectStm (info, bodyOptRef)],
				  stamp)]))
	  | vpBody ([ReturnStm (info, exp)],
		    {env, isToplevel, lazySels, ...}) =
	    let
		val (stms, exp) = vpExp (exp, env, isToplevel, lazySels)
	    in
		stms @ [ReturnStm (info, exp)]
	    end
	  | vpBody ([IndirectStm (info, ref bodyOpt)], state) =
	    vpBody (valOf bodyOpt, state)
	  | vpBody ([ExportStm (info, exp)],
		    {env, isToplevel = true,
		     export = SOME (env', exportDesc), lazySels, ...}) =
	    (*--** do multiple occurrences of ExportStm work by accident? *)
	    let
		val _ =
		    Vector.app (fn (_, id) =>
				declareUnknown (env', IdDef id, true))
		    exportDesc
		val (stms, exp) = vpExp (exp, env, true, lazySels)
	    in
		stms @ [ExportStm (info, exp)]
	    end
	  | vpBody (_, _) =
	    raise Crash.Crash "ValuePropagationPhase.vpBody"
	and vpTestStm (topBody as [TestStm (_, idRef, tests, elseBody)],
		       idRef', state as {env, ...}) =
	    let
		val idRef = deref (idRef, env)
	    in
		if idRefEq (idRef, idRef') then
		    let
			val (testsOpt, elseBody) =
			    vpTestStm (elseBody, idRef', state)
			val (testsOpt', elseBody) =
			    vpTests (idRef, tests, elseBody, state)
		    in
			(testsAppend (testsOpt', testsOpt), elseBody)
		    end
		else (NONE, vpBodyScope (topBody, state))
	    end
	  | vpTestStm (body as [SharedStm (_, body', stamp)],
		       idRef, state as {shared, ...}) =
	    (case StampMap.lookupExistent (shared, stamp) of
		 UNIQUE => vpTestStm (body', idRef, state)
	       | _ => (NONE, vpBodyScope (body, state)))
	  | vpTestStm ([IndirectStm (_, ref bodyOpt)], idRef, state) =
	    vpTestStm (valOf bodyOpt, idRef, state)
	  | vpTestStm (body, _, state) = (NONE, vpBodyScope (body, state))
	and vpTests (idRef, LitTests litBodyVec, elseBody,
		     {env, isToplevel, shared, export, lazySels}) =
	    let
		fun insertId (env, value) =
		    case idRef of
			(IdRef id | LastIdRef id) =>
			    IdMap.lookupExistent (env, id)
			    before IdMap.insert (env, id, (value, isToplevel))
		      | (Lit _ | Prim _) => (UnknownVal, false)
		val (litBodyList, elseBody) =
		    Vector.foldr
		    (fn ((lit, body), (litBodyList, elseBody)) =>
		     let
			 val env = IdMap.cloneTop env
			 val state = {env = env, isToplevel = isToplevel,
				      shared = shared, export = export,
				      lazySels = lazySels}
			 val entry = insertId (env, LitVal lit)
			 val body = vpBody (body, state)
		     in
			 case entry of
			     (LitVal lit', _) =>
				 if litEq (lit, lit') then (nil, body)
				 else (nil, elseBody)
			   | _ => ((lit, body)::litBodyList, elseBody)
		     end) (nil, elseBody) litBodyVec
	    in
		(case litBodyList of
		     nil => NONE
		   | _::_ => SOME (LitTests (Vector.fromList litBodyList)),
		 elseBody)
	    end
	  | vpTests (idRef, TagTests tagBodyVec, elseBody,
		     {env, isToplevel, shared, export, lazySels}) =
	    let
		fun insertId (env, value) =
		    case idRef of
			(IdRef id | LastIdRef id) =>
			    IdMap.lookupExistent (env, id)
			    before IdMap.insert (env, id, (value, isToplevel))
		      | (Lit _ | Prim _) => (UnknownVal, false)
		val (tagBodyList, elseBody) =
		    Vector.foldr
		    (fn ((label, n, args, body), (tagBodyList, elseBody)) =>
		     let
			 val env = IdMap.cloneTop env
			 val state = {env = env, isToplevel = isToplevel,
				      shared = shared, export = export,
				      lazySels = lazySels}
			 val value =
			     TagVal (label, n, mapArgs idDefIdOpt args)
			 val entry = insertId (env, value)
			 val _ = declareArgs (env, args, isToplevel)
		     in
			 case entry of
			     (TagVal (_, n', args'), _) =>
				 if n = n' then
				     (aliasArgs (args, args', env, isToplevel);
				      (nil, vpBody (body, state)))
				 else (nil, elseBody)
			   | (_, _) =>
				 ((label, n, args, vpBody (body, state))::
				  tagBodyList, elseBody)
		     end) (nil, elseBody) tagBodyVec
	    in
		(case tagBodyList of
		     nil => NONE
		   | _::_ => SOME (TagTests (Vector.fromList tagBodyList)),
		 elseBody)
	    end
	  | vpTests (idRef, ConTests conBodyVec, elseBody,
		     {env, isToplevel, shared, export, lazySels}) =
	    let
		fun insertId (env, value) =
		    case idRef of
			(IdRef id | LastIdRef id) =>
			    IdMap.lookupExistent (env, id)
			    before IdMap.insert (env, id, (value, isToplevel))
		      | (Lit _ | Prim _) => (UnknownVal, false)
		val (conBodyList, elseBody) =
		    Vector.foldr
		    (fn ((con, args, body), (conBodyList, elseBody)) =>
		     let
			 val con = deref (con, env)
			 val env = IdMap.cloneTop env
			 val state = {env = env, isToplevel = isToplevel,
				      shared = shared, export = export,
				      lazySels = lazySels}
			 val value =
			     case con of
				 (IdRef id | LastIdRef id) =>
				     ConVal (id, mapArgs idDefIdOpt args)
			       | (Lit _ | Prim _) => UnknownVal
			 val entry = insertId (env, value)
			 val _ = declareArgs (env, args, isToplevel)
		     in
			 case entry of
			     (ConVal (con', args'), _) =>
				 if idRefEq (con, IdRef con') then
				     (aliasArgs (args, args', env, isToplevel);
				      (nil, vpBody (body, state)))
				 else
				     ((con, args, vpBody (body, state))::
				      conBodyList, elseBody)
			   | (_, _) =>
				 ((con, args, vpBody (body, state))::
				  conBodyList, elseBody)
		     end) (nil, elseBody) conBodyVec
	    in
		(case conBodyList of
		     nil => NONE
		   | _::_ => SOME (ConTests (Vector.fromList conBodyList)),
		 elseBody)
	    end
	  | vpTests (idRef, VecTests vecBodyVec, elseBody,
		     {env, isToplevel, shared, export, lazySels}) =
	    let
		fun insertId (env, value) =
		    case idRef of
			(IdRef id | LastIdRef id) =>
			    IdMap.lookupExistent (env, id)
			    before IdMap.insert (env, id, (value, isToplevel))
		      | (Lit _ | Prim _) => (UnknownVal, false)
		val (vecBodyList, elseBody) =
		    Vector.foldr
		    (fn ((idDefs, body), (vecBodyList, elseBody)) =>
		     let
			 val env = IdMap.cloneTop env
			 val state = {env = env, isToplevel = isToplevel,
				      shared = shared, export = export,
				      lazySels = lazySels}
			 val value = VecVal (Vector.map idDefIdOpt idDefs)
			 val entry = insertId (env, value)
			 val _ = declareArgs (env, TupArgs idDefs, isToplevel)
		     in
			 case entry of
			     (VecVal idDefs', _) =>
				 if Vector.length idDefs =
				    Vector.length idDefs'
				 then (nil, vpBody (body, state))
				 else (nil, elseBody)
			   | (_, _) =>
				 ((idDefs, vpBody (body, state))::vecBodyList,
				  elseBody)
		     end) (nil, elseBody) vecBodyVec
	    in
		(case vecBodyList of
		     nil => NONE
		   | _::_ => SOME (VecTests (Vector.fromList vecBodyList)),
		 elseBody)
	    end
	and vpExp (exp as NewExp (_, _), _, _, _) = (nil, exp)
	  | vpExp (exp as VarExp (info, Prim name), _, _, _) =
	    (PrimOps.getArity name
	     handle PrimOps.UnknownPrim =>
		 Error.error (info, "unknown primitive " ^ name);
	     (nil, exp))
	  | vpExp (VarExp (info, idRef), env, _, _) =
	    (nil, varExp (info, idRef, env))
	  | vpExp (TagExp (info, label, n, args), env, _, _) =
	    (nil, TagExp (info, label, n, derefArgs (args, env)))
	  | vpExp (ConExp (info, idRef, args), env, _, _) =
	    (nil, ConExp (info, deref (idRef, env), derefArgs (args, env)))
	  | vpExp (RefExp (info, idRef), env, _, _) =
	    (nil, RefExp (info, deref (idRef, env)))
	  | vpExp (TupExp (info, idRefs), env, _, _) =
	    (nil, TupExp (info, Vector.map (fn idRef => deref (idRef, env))
					   idRefs))
	  | vpExp (ProdExp (info, labelIdRefVec), env, _, _) =
	    (nil, ProdExp (info, Vector.map (fn (label, idRef) =>
						(label, deref (idRef, env)))
					    labelIdRefVec))
	  | vpExp (PolyProdExp (info, labelIdRefVec), env, _, _) =
	    (nil, PolyProdExp (info,
			       Vector.map (fn (label, idRef) =>
					      (label, deref (idRef, env)))
					  labelIdRefVec))
	  | vpExp (VecExp (info, idRefs), env, _, _) =
	    (nil, VecExp (info, Vector.map (fn idRef => deref (idRef, env))
					   idRefs))
	  | vpExp (FunExp (info, stamp, flags, args, body),
		   env, isToplevel, lazySels) =
	    let
		val _ = IdMap.insertScope env
		val _ = declareArgs (env, args, false)
		val body =
		    vpBodyShared (body, stamp,
				  {env = env, isToplevel = false,
				   export = NONE, lazySels = lazySels})
		val flags =
		    if isToplevel then
			if List.exists (fn IsToplevel => true | _ => false)
				       flags then flags
			else IsToplevel::flags
		    else List.filter (fn IsToplevel => false | _ => true) flags
	    in
		IdMap.deleteScope env;
		(nil, FunExp (info, stamp, flags, args,
			      patchArgs (args, lazySels) @
			      patchBody (body, lazySels, StampMap.new ())))
	    end
	  | vpExp (PrimAppExp (info, name, idRefs), env, _, _) =
	    (nil, vpPrimApp (info, name,
			     Vector.map (fn idRef => deref (idRef, env))
					idRefs, env))
	  | vpExp (VarAppExp (info, idRef, args), env, _, _) =
	    let
		val idRef = deref (idRef, env)
		val args = derefArgs (args, env)
	    in
		case lookupEnv (env, idRef) of
		    (PrimVal name, _) =>
			adaptArity (info, valOf (PrimOps.getArity name),
				    args, env,
				    fn args =>
				       vpPrimApp (info, name,
						  argsToIdRefs args, env))
		  | (FunVal (stamp, args', _), true) =>
			(*--** inlining *)
			adaptArity (info, argsToArity args', args, env,
				    fn args => FunAppExp (info, idRef,
							  stamp, args))
		  | (_, _) => (nil, VarAppExp (info, idRef, args))
	    end
	  | vpExp (SelExp (info, prod, label, n, idRef), env, _, _) =
	    let
		val idRef = deref (idRef, env)
	    in
		case doSel (n, idRef, env) of
		    SOME idRef => (nil, VarExp (info, idRef))
		  | NONE => (nil, SelExp (info, prod, label, n, idRef))
	    end
	  | vpExp (LazyPolySelExp (info, label, idRef),
		   env, isToplevel, lazySels) =
	    let
		val idRef = deref (idRef, env)
		val id = idRefId idRef
	    in
		case IdMap.lookupExistent (env, id) of
		    (PolyProdVal map, _) =>
			(case LabelMap.lookup (map, label) of
			     SOME (SOME id) => (nil, VarExp (info, IdRef id))
			   | (SOME NONE | NONE) =>
				 (nil, LazyPolySelExp (info, label, idRef)))
		  | (_, _) =>
			(case IdMap.lookup (lazySels, id) of
			     SOME map =>
				 (nil, VarExp (info,
					       doLazySel (idRef, label, map,
							  env, isToplevel)))
			   | NONE =>
				 let
				     val map = LabelMap.new ()
				 in
				     IdMap.insert (lazySels, id, map);
				     (nil,
				      VarExp (info,
					      doLazySel (idRef, label, map,
							 env, isToplevel)))
				 end)
	    end
	  | vpExp (FunAppExp (info, idRef, stamp, args), env, _, _) =
	    (nil, FunAppExp (info, idRef, stamp, derefArgs (args, env)))
	  | vpExp (FailExp info, _, _, _) = (nil, FailExp info)
	and vpBodyScope (body, {env, isToplevel, shared, export, lazySels}) =
	    vpBody (body, {env = IdMap.cloneTop env, isToplevel = isToplevel,
			   shared = shared, export = export,
			   lazySels = lazySels})
	and vpBodyShared (body, stamp, {env, isToplevel, export, lazySels}) =
	    (case sortShared (body, stamp) of
		 ([stamp']::sorted, shared) =>
		     (Assert.assert (stamp = stamp');
		      vpBody (body, {env = env, isToplevel = isToplevel,
				     shared = shared, export = export,
				     lazySels = lazySels}) before
		      List.app
		      (fn stamps =>
		       let
			   val _ = Assert.assert (List.null (List.tl stamps))
			   val stamp = List.hd stamps
		       in
			   case StampMap.lookupExistent (shared, stamp) of
			       UNIQUE => ()
			     | SHARED =>
				   raise Crash.Crash
				       "ValuePropagationPhase.vpBodyShared 1"
			     | VISITED (bodyOptRef, env) =>
				   let
				       val body = valOf (!bodyOptRef)
				   in
				       bodyOptRef :=
				       SOME (vpBody (body,
						     {env = env,
						      isToplevel = isToplevel,
						      shared = shared,
						      export = export,
						      lazySels = lazySels}))
				   end;
			   StampMap.deleteExistent (shared, stamp)
		       end) sorted)
	       | (_, _) =>
		     raise Crash.Crash "ValuePropagationPhase.vpBodyShared 2")

	fun debug component =
	    let
		val q = TextIO.openOut "vpdebug.txt"
	    in
		TextIO.output (q,
			       "\n" ^
			       OutputFlatGrammar.componentToString component ^
			       "\n");
		TextIO.closeOut q
	    end

	fun translate env (_, component as (imports, body, exportDesc, sign)) =
	    let
		val env' = IdMap.clone env
		val _ =
		    Vector.app (fn (id, _, _) =>
				declareUnknown (env', IdDef id, true)) imports
		val _ = IdMap.insertScope env'
		val lazySels = IdMap.new ()
		val body =
		    vpBodyShared (body, Stamp.new (),
				  {env = env', isToplevel = true,
				   export = SOME (env, exportDesc),
				   lazySels = lazySels})
	    in
		(imports,
		 IdMap.foldi (fn (id, _, rest) =>
			      patchIdDef (IdDef id, lazySels) @ rest) nil env @
		 patchIdDefs (Vector.map (fn (id, _, _) => IdDef id) imports,
			      lazySels) @
		 patchBody (body, lazySels, StampMap.new ()),
		 exportDesc, sign)
	    end
	    (*--**DEBUG*)
	    handle exn as Error.Error (_, _) => raise exn
		 | exn =>
		       (TextIO.print
			"\nValuePropagationPhase crashed: \
			 \dumping debug information to vpdebug.txt\n";
			debug component;
			case exn of
			    IdMap.Lookup id =>
				TextIO.print ("Lookup " ^
					      OutputFlatGrammar.idToString id ^
					      "\n")
			  | _ => ();
			raise exn)

	open PrettyPrint
	infix ^/^

	fun dumpContext env =
	    hbox (text "{" ^/^
		  fbox (below (IdMap.foldi
			       (fn (id, _, rest) =>
				text (OutputFlatGrammar.idToString id) ^/^
				rest) (text "}") env)))
    end
