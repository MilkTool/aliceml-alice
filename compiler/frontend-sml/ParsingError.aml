(*
 * Author:
 *   Andreas Rossberg <rossberg@ps.uni-sb.de>
 *
 * Copyright:
 *   Andreas Rossberg, 2001
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import structure Error         from "../infrastructure/Error"
import structure PrettyPrint   from "../misc/PrettyPrint"
import structure PPMisc        from "../misc/PPMisc"
import structure Source        from "../infrastructure/Source"
import structure VId           from "Ids"
import signature PARSING_ERROR from "PARSING_ERROR-sig"


structure ParsingError :> PARSING_ERROR =
struct

  (* Pretty printer *)

    open PrettyPrint
    open PPMisc

    infixr ^^ ^/^

  (* Types *)

    datatype error =
	(* Lexer *)
	  UnclosedComment
	| InvalidChar		of char
	| InvalidString
	| IntTooLarge
	| WordTooLarge
	| RealTooLarge
	| CharLengthInvalid	of string
	| EscapeCharTooLarge	of bool
	(* Parser *)
	| SyntaxError		of string
	(* Derived forms *)
	| WithtypeInvalid
	| WithtypeArityMismatch


    type warning = unit		(* yet empty *)


  (* Pretty printing *)

    fun ppQuoted s	= "`" ^ s ^ "'"

    fun ppError(UnclosedComment) =
	  textpar["unclosed","comment"]
      | ppError(InvalidChar c) =
	  textpar["invalid","character",ppQuoted(Char.toCString c)]
      | ppError(InvalidString) =
	  textpar["invalid","string","constant"]
      | ppError(IntTooLarge) =
	  textpar["integer","constant","too","large"]
      | ppError(WordTooLarge) =
	  textpar["word","constant","too","large"]
      | ppError(RealTooLarge) =
	  textpar["real","constant","too","large"]
      | ppError(CharLengthInvalid "") =
	  textpar["empty","character","constant"]
      | ppError(CharLengthInvalid s) =
	  textpar["multiple","characters","in","character","constant"]
      | ppError(EscapeCharTooLarge uc) =
	  textpar[if uc then "unicode" else "ASCII",
		  "escape","character","too","large"]
      (* Parser *)
      | ppError(SyntaxError s) =
	  textpar(String.tokens (fn c => c = #" ") s)
      (* Derived forms *)
      | ppError(WithtypeInvalid) =
	  textpar["invalid","type","binding","inside","withtype"]
      | ppError(WithtypeArityMismatch) =
	  textpar["type","has","wrong","arity"]


    fun ppWarning w = empty


  (* Export *)

    fun errorToString e   = PrettyPrint.toString(ppError e, 75)
    fun warningToString w = PrettyPrint.toString(ppWarning w, 75)

    fun error(region, e)  = Error.error(region, errorToString e)
    fun warn(region, w)   = Error.warn(region, warningToString w)

end
