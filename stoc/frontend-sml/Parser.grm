(*										*)
(* Standard ML syntactical analysis						*)
(*										*)
(* Definition, sections 2, 3, and 8, Appendix A and B				*)
(*										*)
(* Extensions:									*)
(*   - allow = to be bound as vid (in a pattern it must be prefixed by op)	*)
(*   - record update expressions:						*)
(*	  atexp ::= { atexp where exprow }					*)
(*   - vector expressions and patterns:						*)
(*	  atexp ::= #[ exp_1 , ... , exp_n ] 	(n>=0)				*)
(*	  atpat ::= #[ pat_1 , ... , pat_n ]	(n>=0)				*)
(*   - punning in record expressions (derived form):				*)
(*	  exprow ::= vid <: ty> <, exprow>					*)
(*   - recursive expressions (derived form):					*)
(*	  exp ::= rec pat => exp						*)
(*   - more relaxed constructor pattern syntax:					*)
(*	  pat ::= pat atpat			(R)				*)
(*   - generalized layered patterns:						*)
(*	  pat ::= pat as pat			(R)				*)
(*   - alternative and guarded patterns:					*)
(*	  atpat ::= ( pat_1 | ... | pat_n )	(n>=2)				*)
(*	  pat   ::= pat where atexp		(L)				*)
(*   - negated patterns:							*)
(*	  pat ::= non pat							*)
(*   - with patterns:								*)
(*	  pat ::= pat withval valbind end					*)
(*	      ::= pat withfun fvalbind end	(derived form)			*)
(*   - open datatypes (exception declarations become a derived form),		*)
(*     constructor synonym specifications:					*)
(*        dec      ::= constructor dconbind					*)
(*	  datbind  ::= tyvarseq tycon						*)
(*	  dconbind ::= <op> vid <of ty> : tyvarseq longtycon <and dconbind>	*)
(*		   ::= <op> vid = <op> longvid <and dconbind>			*)
(*        spec     ::= constructor dcondesc					*)
(*	  datdesc  ::= tyvarseq tycon						*)
(*	  dcondesc ::= vid <of ty> : tyvarseq longtycon <and dcondesc>		*)
(*		   ::= vid = longvid <and dcondesc>				*)
(*   - abstract type declartions:						*)
(*	  typbind  ::= tyvarseq tycon						*)
(*	  dec      ::= eqtype typbind						*)
(*   - straightified type specifications:					*)
(*	  typdesc ::= tyvarseq tycon						*)
(*		      tyvarseq tycon = ty					*)
(*   - where for structures:							*)
(*	  sigexp ::= sigexp where longstrid_1 = longstrid_2			*)
(*   - definitional structure specifications:					*)
(*	  strdesc ::= strid <: sigexp> = longstrid <and strdesc>		*)
(*   - parenthesised structure and signature expressions (derived forms):	*)
(*        strexp ::= ( strexp )							*)
(*        sigexp ::= ( sigexp )							*)
(*   - fun keyword in signatures (derived form):				*)
(*	  spec ::= fun valdesc							*)
(*   - op keyword in signatures:						*)
(*	  valdesc  ::= <op> vid : ty <and valdesc>				*)
(*	  condesc  ::= <op> vid <of ty> <| condesc>				*)
(*	  exdesc   ::= <op> vid <of ty> <and exdesc>				*)
(*	  dcondesc ::= <op> vid <of ty> : tyvarseq longtycon <and dcondesc>	*)
(*		   ::= <op> vid = <op> longvid <and dcondesc>			*)
(*   - withtype in signatures (derived form):					*)
(*	  spec ::= datatype datdesc <withtype syndesc>				*)
(*   - fixity directives in signatures:						*)
(*	  spec ::= infix <d> vid_1 ... vid_n	(n>=1)				*)
(*		   infixr <d> vid_1 ... vid_n	(n>=1)				*)
(*		   nonfix vid_1 ... vid_n	(n>=1)				*)
(*   - no distinction between dec, strdec, and topdec				*)
(*   - long functor identifiers:						*)
(*	  strexp  ::= longfunid ( strexp )					*)
(*	          ::= longfunid ( dec )						*)
(*   - long signature identifiers:						*)
(*	  sigexp  ::= longsigid							*)
(*	  spec    ::= include longsigid_1 ... longsigid_n			*)
(*   - functor specifications:							*)
(*	  spec    ::= functor fundesc						*)
(*	  fundesc ::= funid ( strid : sigexp ) : sigexp <and fundesc>		*)
(*	          ::= funid ( spec ) : sigexp <and fundesc>			*)
(*   - signature specifications:						*)
(*	  spec    ::= signature sigdesc						*)
(*	  sigdesc ::= sigid <and sigdesc>					*)
(*	          ::= sigid = sigexp <and sigdesc>				*)
(*   - sharing and where for signature:						*)
(*	  spec   ::= spec sharing signature longsigid_1 = ... = longsigid_n	*)
(*	  sigexp ::= sigexp where signature longsigid_1 = sigexp		*)
(*   - components:								*)
(*	  component ::= import <program>					*)
(*	  import    ::= import spec from string					*)
(*			<>							*)
(*			import <;> import					*)
(*										*)
(* We did NOT introduce a sharing signature ... and signature ... derived form	*)
(* similar to types, because we consider that completely broken.		*)
(*										*)
(* To avoid hardwiring certain library issues, we further added the following	*)
(* low-level constructs:							*)
(*	  dec     ::= __prebound strid						*)
(*	          ::= __primitive val <op> vid : ty = string			*)
(*	          ::= __primitive constructor <op> vid <of ty> : ty = string	*)
(*	          ::= __primitive exception <op> vid <of ty> = string (DF)	*)
(*	          ::= __primitive structure strid : sigexp = string		*)
(*	          ::= __primitive functor funid ( strid : sigexp ) : sigexp	*)
(*						 		   = string	*)
(*	          ::= __primitive functor funid ( spec ) : sigexp = string (DF)	*)
(*	          ::= __overload <op> vid with tyvar : ty			*)
(*	          ::= __instance <op> vid with longtycon = longvid		*)
(*	          ::= __instance scon with longtycon				*)
(*	          ::= __eqtype typbind						*)
(*	  spec    ::= __eqtype typdesc						*)
(*	          ::= __prebound strid						*)
(*	          ::= __overload <op> vid with tyvar : ty			*)
(*	          ::= __instance <op> vid with longtycon = longvid		*)
(*	          ::= __instance scon with longtycon				*)
(*										*)
(* Notes:									*)
(*   - Two phrases named Fmatch and Fmrule have been added to factorize		*)
(*     Fvalbind.								*)
(*   - A phrase named SynDesc has been added to factorize type synonym		*)
(*     specifications. Similarly, a phrase named TyReaDesc has been added to	*)
(*     factorize type realisation signature expressions.			*)
(*   - Infix expressions [Definition, section 2.6] are resolved during		*)
(*     elaboration because we allow fixity specifications in signatures that	*)
(*     come into effect by an open declaration. Infix status is completely	*)
(*     ignored here.								*)
(*   - Syntactic restrictions [Definition, sections 2.9 and 3.5] are checked	*)
(*     during elaboration, as well as the Fvalbind derived form.		*)
(*   - The Definition is not clear about whether `=' should also be legal as	*)
(*     a tycon. Since this would result in massive conflicts, and a type named	*)
(*     `=' could only be used legally if an implementation would be mad enough	*)
(*     to predefine it anyway, we simply disallow it.				*)
(*   - Datatype replication requires rules for datatype to be duplicated to	*)
(*     avoid conflicts on empty tyvarseqs.					*)
(*   - The messy `sigexp where type ... and type ...' syntax requires some	*)
(*     really ugly transformations (in absence of a lookahead of 2), watch out	*)
(*     for non-terminals of the form xxx__AND_yyybind_opt.			*)
(*   - We do NOT support declarations like					*)
(*	  fun f p1 = case e1 of p2 => e2					*)
(*	    | f p3 = e3								*)
(*     (without parentheses around the case) because the transformations	*)
(*     required to support this would be even a magnitude uglier than those	*)
(*     above. In fact, no compiler I know of supports this.			*)
(*   - ML-Yacc does not seem to like comments that stretch over several		*)
(*     lines... Similarly, comments in semantic actions make it puke...		*)
(*										*)



    (* Import *)

    open InputGrammar
    open DerivedForms


    (* Helper to build position fields *)

    fun I(left, right) = (left, right)


%%


%header	(functor LrVals(structure Token:        TOKEN
			structure DerivedForms: DERIVED_FORMS
		       )
	)

%name Parser

%pos  Source.pos

%verbose



%term	(* End of file *)
	  EOF

	(* Reserved words for the core language *)
	| ABSTYPE | AND | ANDALSO | AS | CASE | DO | DATATYPE | ELSE
	| END | EXCEPTION | FN | FUN | HANDLE | IF | IN | INFIX
	| INFIXR | LET | LOCAL | NONFIX | OF | OP | OPEN | ORELSE
	| RAISE | REC | THEN | TYPE | VAL | WITH | WITHTYPE | WHILE
	| LPAR | RPAR | LBRACK | RBRACK | LBRACE | RBRACE | COMMA | COLON
	| SEMICOLON | DOTS | UNDERBAR | BAR | EQUALS | DARROW | ARROW | HASH
	| CONSTRUCTOR | NON | WITHFUN | WITHVAL | DOT | HASHBRACK
	| IMPORT | FROM
	| PRIMITIVE | OVERLOAD | INSTANCE | PREBOUND | EQEQTYPE

	(* Additional reserved words for the modules language *)
	| EQTYPE | FUNCTOR | INCLUDE | SHARING | SIG
	| SIGNATURE | STRUCT | STRUCTURE | WHERE | COLONGREATER

	(* Special constants *)
	| ZERO | DIGIT of int | NUMERIC of LargeInt.int
	| INT of LargeInt.int | WORD of LargeWord.word
	| REAL of LargeReal.real
	| STRING of WideString.string | CHAR of WideChar.char

	(* Identifiers *)
	| ALPHA of string | SYMBOL of string | STAR
	| TYVAR of string | ETYVAR of string

%keyword ABSTYPE AND ANDALSO AS CASE DO DATATYPE ELSE
	 END EXCEPTION FN FUN HANDLE IF IN INFIX
	 INFIXR LET LOCAL NONFIX OF OP OPEN ORELSE
	 RAISE REC THEN TYPE VAL WITH WITHTYPE WHILE
	 EQTYPE FUNCTOR INCLUDE SHARING SIG
	 SIGNATURE STRUCT STRUCTURE WHERE
	 CONSTRUCTOR NON WITHFUN WITHVAL
	 IMPORT FROM
	 PRIMITIVE OVERLOAD INSTANCE PREBOUND EQEQTYPE

%eop     EOF
%noshift EOF


%left  SHARING
%left  SEMICOLON
%right VAL FUN TYPE EQTYPE DATATYPE ABSTYPE CONSTRUCTOR EXCEPTION
       STRUCTURE SIGNATURE FUNCTOR
       IMPORT
       PRIMITIVE OVERLOAD INSTANCE PREBOUND EQEQTYPE
%right LOCAL OPEN INFIX INFIXR NONFIX INCLUDE
%right AND
%left  DARROW		(* L/R is arbitrary *)
%left  BAR		(* L/R is arbitrary *)
%left  DO		(* L/R is arbitrary *)
%left  ELSE		(* L/R is arbitrary *)
%left  RAISE		(* L/R is arbitrary *)
%right HANDLE
%right ORELSE
%right ANDALSO
%left  WHERE
%left  WITHVAL
%left  WITHFUN
%right AS
%right NON		(* L/R is arbitrary *)
%left  COLON
%right ARROW


%start component

%nonterm
	(* Constants *)
	  scon				of SCon
	| d				of int

	(* Identifiers and labels *)
	| lab				of Lab
	| vid				of VId
	|    vid'			of VId		(* excludes `=' *)
	| tycon				of TyCon
	| tyvar				of TyVar
	| strid				of StrId
	| sigid				of SigId
	| funid				of FunId

	| longvid			of LongVId
	|    longvid'			of LongVId	(* excludes `=' *)
	| longtycon			of LongTyCon
	| longstrid			of LongStrId
	| longsigid			of LongSigId
	| longfunid			of LongFunId

	| OP_opt			of Op

	(* Nonterminals of the core language *)
	| atexp				of AtExp
	|    exp_COMMA_list0		of Exp list
	|    exp_COMMA_list1		of Exp list
	|    exp_COMMA_list2		of Exp list
	|    exp_SEMICOLON_list1	of Exp list
	|    exp_SEMICOLON_list2	of Exp list
	| exprow			of ExpRow
	|    exprow_opt			of ExpRow option
	|    COMMA_exprow_opt		of ExpRow option
	| appexp			of AppExp
	| infexp			of InfExp
	| exp				of Exp

	| match				of Match
	|    BAR_match_opt		of Match option
	| mrule				of Mrule

	| dec				of Dec
	|    dec'			of Dec (* excludes semicolons *)
	|    dec1			of Dec
	|    dec1'			of Dec (* excludes semicolons *)
	|    dec1''			of Dec (* excludes local and seq. *)
	|    WITHTYPE_typbind_opt	of TypBind option
	|    vid_list1			of VId list
	|    longstrid_list1		of LongStrId list
	|    d_opt			of int option
	| valbind			of ValBind
	|    AND_valbind_opt		of ValBind option
	| fvalbind			of FvalBind
	|    AND_fvalbind_opt		of FvalBind option
	| fmatch			of Fmatch
	|    BAR_fmatch_opt		of Fmatch option
	| fmrule			of Fmrule
	| fpat				of Fpat
	| typbind			of TypBind
	|    AND_typbind_opt		of TypBind option
	| datbind			of DatBind
	|    datbind0			of DatBind
	|    datbind1			of DatBind
	|    AND_datbind_opt		of DatBind option
	| conbind			of ConBind
	|    BAR_conbind_opt		of ConBind option
	|    OF_ty_opt			of Ty option
	| dconbind			of DconBind
	|    AND_dconbind_opt		of DconBind option
	| exbind			of ExBind
	|    AND_exbind_opt		of ExBind option

	| atpat				of AtPat
	|    pat_COMMA_list0		of Pat list
	|    pat_COMMA_list1		of Pat list
	|    pat_COMMA_list2		of Pat list
	|    pat_BAR_list2		of Pat list
	| patrow			of PatRow
	|    patrow_opt			of PatRow option
	|    COMMA_patrow_opt		of PatRow option
	|    COLON_ty_opt		of Ty option
	|    AS_pat_opt			of Pat option
	| infpat			of Pat
	| pat				of Pat

	| ty				of Ty
	|    tupty			of Ty
	|    ty_STAR_list		of Ty list
	|    consty			of Ty
	|    atty			of Ty
	| tyrow				of TyRow
	|    tyrow_opt			of TyRow option
	|    COMMA_tyrow_opt		of TyRow option
	| tyseq				of TySeq
	|    ty_COMMA_list2		of Ty list
	| tyvarseq			of TyVarSeq
	|    tyvarseq1			of TyVarSeq
	|    tyvar_COMMA_list1		of TyVar list

	(* Nonterminals for the module language *)
	| strexp			of StrExp
	|    strexp'			of StrExp (* excludes constraints *)
	| strbind			of StrBind
	|    AND_strbind_opt		of StrBind option
	|    strexp__AND_strbind_opt	of StrExp * StrBind option
	|    sigexp__AND_strbind_opt	of SigExp * StrBind option
	|    tyreadesc__AND_strbind_opt	of TyReaDesc * StrBind option
	|    AND_tyreadesc_opt__AND_strbind_opt
					of TyReaDesc option * StrBind option
	|    COLON_sigexp_opt		of SigExp option

	| sigexp			of SigExp
	|    sigexp'			of SigExp (* excludes where *)
	| sigbind			of SigBind
	|    AND_sigbind_opt		of SigBind option
	|    sigexp__AND_sigbind_opt	of SigExp * SigBind option
	|    tyreadesc__AND_sigbind_opt	of TyReaDesc * SigBind option
	|    AND_tyreadesc_opt__AND_sigbind_opt
					of TyReaDesc option * SigBind option
	| tyreadesc			of TyReaDesc
	|    AND_tyreadesc_opt		of TyReaDesc option
	| funbind			of FunBind
	|    AND_funbind_opt		of FunBind option
	|    strexp__AND_funbind_opt	of StrExp * FunBind option
	|    sigexp__AND_funbind_opt	of SigExp * FunBind option
	|    tyreadesc__AND_funbind_opt	of TyReaDesc * FunBind option
	|    AND_tyreadesc_opt__AND_funbind_opt
					of TyReaDesc option * FunBind option

	| spec				of Spec
	|    spec1			of Spec
	|    spec1'			of Spec (* excludes sharing and seq. *)
	|    WITHTYPE_typdesc_opt	of TypDesc option
	|    longsigid_list2		of LongSigId list
	|    longtycon_EQUALS_list1	of LongTyCon list
	|    longtycon_EQUALS_list2	of LongTyCon list
	|    longsigid_EQUALS_list1	of LongSigId list
	|    longsigid_EQUALS_list2	of LongSigId list
	|    longstrid_EQUALS_list1	of LongStrId list
	|    longstrid_EQUALS_list2	of LongStrId list

	| valdesc			of ValDesc
	|    AND_valdesc_opt		of ValDesc option
	| typdesc			of TypDesc
	|    AND_typdesc_opt		of TypDesc option
	| datdesc			of DatDesc
	|    datdesc0			of DatDesc
	|    datdesc1			of DatDesc
	|    AND_datdesc_opt		of DatDesc option
	| condesc			of ConDesc
	|    BAR_condesc_opt		of ConDesc option
	| dcondesc			of DconDesc
	|    AND_dcondesc_opt		of DconDesc option
	| exdesc			of ExDesc
	|    AND_exdesc_opt		of ExDesc option
	| strdesc			of StrDesc
	|    AND_strdesc_opt		of StrDesc option
	|    sigexp__AND_strdesc_opt	of SigExp * StrDesc option
	|    tyreadesc__AND_strdesc_opt	of TyReaDesc * StrDesc option
	|    AND_tyreadesc_opt__AND_strdesc_opt
					of TyReaDesc option * StrDesc option
	| sigdesc			of SigDesc
	|    AND_sigdesc_opt		of SigDesc option
	|    sigexp__AND_sigdesc_opt	of SigExp * SigDesc option
	|    tyreadesc__AND_sigdesc_opt	of TyReaDesc * SigDesc option
	|    AND_tyreadesc_opt__AND_sigdesc_opt
					of TyReaDesc option * SigDesc option
	| fundesc			of FunDesc
	|    AND_fundesc_opt		of FunDesc option
	|    strexp__AND_fundesc_opt	of StrExp * FunDesc option
	|    sigexp__AND_fundesc_opt	of SigExp * FunDesc option
	|    tyreadesc__AND_fundesc_opt	of TyReaDesc * FunDesc option
	|    AND_tyreadesc_opt__AND_fundesc_opt
					of TyReaDesc option * FunDesc option

	(* Top nonterminal *)
	| program			of Program
	|    program_opt		of Program option
	|    program_opt'		of Program option
	| component			of Component
	| import0			of Import
	|    import1			of Import


%%


  (* Constants *)

  scon:
	  ZERO		( SCon(I(ZEROleft,ZEROright),   SCon.INT 0) )
	| DIGIT		( SCon(I(DIGITleft,DIGITright),
				SCon.INT(LargeInt.fromInt DIGIT)) )
	| NUMERIC	( SCon(I(NUMERICleft,NUMERICright),
							SCon.INT NUMERIC) )
	| INT		( SCon(I(INTleft,INTright),     SCon.INT INT) )
	| WORD		( SCon(I(WORDleft,WORDright),   SCon.WORD WORD) )
	| STRING	( SCon(I(STRINGleft,STRINGright),
							SCon.STRING STRING))
	| CHAR		( SCon(I(CHARleft,CHARright),   SCon.CHAR CHAR) )
	| REAL		( SCon(I(REALleft,REALright),   SCon.REAL REAL) )

  d:
	  ZERO		( 0 )
	| DIGIT		( DIGIT )



  (* Identifiers and labels *)

  lab:
	  ALPHA		( Lab(I(ALPHAleft,ALPHAright),   Lab.fromString ALPHA) )
	| SYMBOL	( Lab(I(SYMBOLleft,SYMBOLright), Lab.fromString SYMBOL))
	| STAR		( Lab(I(STARleft,STARright),     Lab.fromString "*") )
	| DIGIT		( Lab(I(DIGITleft,DIGITright),   Lab.fromInt DIGIT) )
	| NUMERIC	( Lab(I(NUMERICleft,NUMERICright),
						Lab.fromLargeInt NUMERIC))


  vid:
	  vid'		( vid' )
	| EQUALS	( VId(I(EQUALSleft,EQUALSright), VId.fromString "=") )

     vid':
	  ALPHA		( VId(I(ALPHAleft,ALPHAright),   VId.fromString ALPHA) )
	| SYMBOL	( VId(I(SYMBOLleft,SYMBOLright), VId.fromString SYMBOL))
	| STAR		( VId(I(STARleft,STARright),     VId.fromString "*") )

  tycon:
	  ALPHA		( TyCon(I(ALPHAleft,ALPHAright),
				TyCon.fromString ALPHA) )
	| SYMBOL	( TyCon(I(SYMBOLleft,SYMBOLright),
				TyCon.fromString SYMBOL) )

  tyvar:  TYVAR		( TyVar(I(TYVARleft,TYVARright),
				TyVar.fromString TYVAR) )

  strid:  ALPHA		( StrId(I(ALPHAleft,ALPHAright),
				StrId.fromString ALPHA) )

  sigid:  ALPHA		( SigId(I(ALPHAleft,ALPHAright),
				SigId.fromString ALPHA) )

  funid:  ALPHA		( FunId(I(ALPHAleft,ALPHAright),
				FunId.fromString ALPHA) )


  longvid:
	  longvid'		( longvid' )
	| EQUALS		( SHORTLong(I(EQUALSleft,EQUALSright),
					    VId(I(EQUALSleft,EQUALSright),
						VId.fromString "=")) )
     longvid':
	  vid'			( SHORTLong(I(vid'left,vid'right), vid') )
	| longstrid DOT vid	( DOTLong(I(longstridleft,vidright),
					  longstrid, vid) )
  longtycon:
	  tycon			( SHORTLong(I(tyconleft,tyconright), tycon) )
	| longstrid DOT tycon	( DOTLong(I(longstridleft,tyconright),
					  longstrid, tycon) )
  longstrid:
	  strid			( SHORTLong(I(stridleft,stridright), strid) )
	| longstrid DOT strid	( DOTLong(I(longstridleft,stridright),
					  longstrid, strid) )
  longsigid:
	  sigid			( SHORTLong(I(sigidleft,sigidright), sigid) )
	| longstrid DOT sigid	( DOTLong(I(longstridleft,sigidright),
					  longstrid, sigid) )
  longfunid:
	  funid			( SHORTLong(I(funidleft,funidright), funid) )
	| longstrid DOT funid	( DOTLong(I(longstridleft,funidright),
					  longstrid, funid) )


  OP_opt:
	  OP			( WITHOp )
	| (*empty*)		( SANSOp )




  (* Core: Expressions *)

  atexp:
	  scon		( SCONAtExp(I(sconleft,sconright), scon) )
	| longvid
			( LONGVIDAtExp(I(longvidleft,longvidright),
				       SANSOp, longvid) )
	| OP longvid
			( LONGVIDAtExp(I(OPleft,longvidright),
				       WITHOp, longvid) )
	| LBRACE exprow_opt RBRACE
			( RECORDAtExp(I(LBRACEleft,RBRACEright), exprow_opt) )
	| LBRACE atexp WHERE exprow RBRACE
			( UPDATEAtExp(I(LBRACEleft,RBRACEright), atexp, exprow))
	| HASH lab	( HASHAtExp(I(HASHleft,labright), lab) )
	| LPAR RPAR	( UNITAtExp(I(LPARleft,RPARright)) )
	| LPAR exp_COMMA_list2 RPAR
			( TUPLEAtExp(I(LPARleft,RPARright), exp_COMMA_list2) )
	| LBRACK exp_COMMA_list0 RBRACK
			( LISTAtExp(I(LBRACKleft,RBRACKright),
				    exp_COMMA_list0 ))
	| HASHBRACK exp_COMMA_list0 RBRACK
			( VECTORAtExp(I(HASHBRACKleft,RBRACKright),
				    exp_COMMA_list0 ))
	| LPAR exp_SEMICOLON_list2 RPAR
			( SEQAtExp(I(LPARleft,RPARright), exp_SEMICOLON_list2) )
	| LET dec IN exp_SEMICOLON_list1 END
			( LETAtExp(I(LETleft,ENDright),
				   dec, exp_SEMICOLON_list1) )
	| LPAR exp RPAR	( PARAtExp(I(LPARleft,RPARright), exp) )


     exp_COMMA_list0:
	  exp_COMMA_list1		( exp_COMMA_list1 )
	| (*empty*)			( [] )

     exp_COMMA_list1:
	  exp COMMA exp_COMMA_list1	( exp::exp_COMMA_list1 )
	| exp				( exp::[] )

     exp_COMMA_list2:
	  exp COMMA exp_COMMA_list1	( exp::exp_COMMA_list1 )

     exp_SEMICOLON_list1:
	  exp SEMICOLON exp_SEMICOLON_list1	( exp::exp_SEMICOLON_list1 )
	| exp					( exp::[] )

     exp_SEMICOLON_list2:
	  exp SEMICOLON exp_SEMICOLON_list1	( exp::exp_SEMICOLON_list1 )


  exprow:
	  lab EQUALS exp COMMA_exprow_opt
	  			( ROWExpRow(I(lableft,COMMA_exprow_optright),
	  				    lab, exp, COMMA_exprow_opt) )
	| vid' COLON_ty_opt COMMA_exprow_opt
	  			( VIDExpRow(I(vid'left,COMMA_exprow_optright),
	  				    vid', COLON_ty_opt,
					    COMMA_exprow_opt) )
     COMMA_exprow_opt:
	  COMMA exprow		( SOME exprow )
	| (*empty*)		( NONE )

     exprow_opt:
	  exprow		( SOME exprow )
	| (*empty*)		( NONE )


  appexp:
	  atexp			( ATEXPExp(I(atexpleft,atexpright), atexp) )
	| appexp atexp		( APPExp(I(appexpleft,atexpright),
					 appexp, atexp) )


  infexp:
	  appexp		( appexp )
	(*infexp vid infexp	( included above... )*)


  exp:
	  infexp		( infexp )
	| exp COLON ty		( TYPEDExp(I(expleft,tyright), exp, ty) )
	| exp ANDALSO exp	( ANDALSOExp(I(exp1left,exp2right), exp1, exp2))
	| exp ORELSE exp	( ORELSEExp(I(exp1left,exp2right), exp1, exp2) )
	| exp HANDLE match	( HANDLEExp(I(expleft,matchright), exp, match) )
	| RAISE exp		( RAISEExp(I(RAISEleft,expright), exp) )
	| IF exp THEN exp ELSE exp
				( IFExp(I(IFleft,exp3right), exp1, exp2, exp3) )
	| WHILE exp DO exp	( WHILEExp(I(WHILEleft,exp2right), exp1, exp2) )
	| CASE exp OF match	( CASEExp(I(CASEleft,matchright), exp, match) )
	| FN match		( FNExp(I(FNleft,matchright), match) )
	| REC pat DARROW exp	( RECExp(I(RECleft,expright), pat, exp) )



  (* Core: Matches *)

  match:
	  mrule BAR_match_opt	( Match(I(mruleleft,BAR_match_optright),
					mrule, BAR_match_opt) )

     BAR_match_opt:
	  BAR match			( SOME match )
	| (*empty*) %prec DARROW	( NONE )

  mrule:
	  pat DARROW exp	( Mrule(I(patleft,expright), pat, exp) )



  (* Core: Declarations *)

  dec:
	  dec1		( dec1 )
	| (*empty*)	( EMPTYDec(I(defaultPos,defaultPos)) )

     dec':
	  dec1'		( dec1' )
	| dec' dec' %prec SEMICOLON
			( SEQDec(I(dec'1left,dec'2right), dec'1, dec'2) )

     dec1:
	  dec1'		( dec1' )
	| SEMICOLON	( EMPTYDec(I(SEMICOLONleft,SEMICOLONleft)) )
	| dec1 dec1 %prec SEMICOLON
			( SEQDec(I(dec11left,dec12right), dec11, dec12) )

     dec1':
	  dec1''	( dec1'' )
	| LOCAL dec IN dec END
			( LOCALDec(I(LOCALleft,ENDright), dec1, dec2) )

     dec1'':
	  VAL valbind	( VALDec(I(VALleft,valbindright),
				 Seq(I(defaultPos,defaultPos), []), valbind) )
	| VAL tyvarseq1 valbind
			( VALDec(I(VALleft,valbindright), tyvarseq1, valbind) )
	| FUN fvalbind	( FUNDec(I(FUNleft,fvalbindright),
				 Seq(I(defaultPos,defaultPos), []), fvalbind) )
	| FUN tyvarseq1 fvalbind
			( FUNDec(I(FUNleft,fvalbindright), tyvarseq1, fvalbind))
	| TYPE typbind	( TYPEDec(I(TYPEleft,typbindright), typbind) )
	| EQTYPE typbind
			( EQTYPEDec(I(EQTYPEleft,typbindright), typbind) )
	| EQEQTYPE typbind
			( EQEQTYPEDec(I(EQEQTYPEleft,typbindright), typbind) )
	| DATATYPE datbind0 WITHTYPE_typbind_opt
			( DATATYPEDec(I(DATATYPEleft,WITHTYPE_typbind_optright),
				      datbind0, WITHTYPE_typbind_opt) )
	| DATATYPE datbind1 WITHTYPE_typbind_opt
			( DATATYPEDec(I(DATATYPEleft,WITHTYPE_typbind_optright),
				      datbind1, WITHTYPE_typbind_opt) )
	| DATATYPE tycon EQUALS DATATYPE longtycon
			( REPLICATIONDec(I(DATATYPEleft,longtyconright),
					 tycon, longtycon) )
	| ABSTYPE datbind WITHTYPE_typbind_opt WITH dec END
			( ABSTYPEDec(I(ABSTYPEleft,ENDright), datbind,
				     WITHTYPE_typbind_opt, dec) )
	| CONSTRUCTOR dconbind
			( CONSTRUCTORDec(I(CONSTRUCTORleft,dconbindright),
					 dconbind) )
	| EXCEPTION exbind
			( EXCEPTIONDec(I(EXCEPTIONleft,exbindright), exbind) )
	| STRUCTURE strbind
			( STRUCTUREDec(I(STRUCTUREleft,strbindright), strbind) )
	| SIGNATURE sigbind
			( SIGNATUREDec(I(SIGNATUREleft,sigbindright), sigbind) )
	| FUNCTOR funbind
			( FUNCTORDec(I(FUNCTORleft,funbindright), funbind) )
	| OPEN longstrid_list1
			( OPENMULTIDec(I(OPENleft,longstrid_list1right),
				       longstrid_list1) )
	| PREBOUND strid
			( PREBOUNDDec(I(PREBOUNDleft,stridright), strid) )
	| PRIMITIVE VAL OP_opt vid COLON ty EQUALS STRING
			( PRIMITIVEVALDec(I(PRIMITIVEleft,STRINGright),
					  OP_opt, vid, ty, STRING) )
	| PRIMITIVE CONSTRUCTOR OP_opt vid OF_ty_opt COLON tyvarseq longtycon
	  EQUALS STRING
			( PRIMITIVECONSTRUCTORDec(I(PRIMITIVEleft,STRINGright),
						  OP_opt, vid, OF_ty_opt,
						  tyvarseq, longtycon, STRING) )
	| PRIMITIVE EXCEPTION OP_opt vid OF_ty_opt EQUALS STRING
			( PRIMITIVEEXCEPTIONDec(I(PRIMITIVEleft,STRINGright),
						OP_opt, vid, OF_ty_opt, STRING))
	| PRIMITIVE STRUCTURE strid COLON sigexp EQUALS STRING
			( PRIMITIVESTRUCTUREDec(I(PRIMITIVEleft,STRINGright),
						strid, sigexp, STRING) )
	| PRIMITIVE FUNCTOR funid LPAR strid COLON sigexp RPAR COLON sigexp
	  EQUALS STRING
			( PRIMITIVEFUNCTORDec(I(PRIMITIVEleft,STRINGright),
					      funid, strid, sigexp1, sigexp2,
					      STRING) )
	| PRIMITIVE FUNCTOR funid LPAR spec RPAR COLON sigexp EQUALS STRING
			( PRIMITIVEFUNCTORSPECDec(I(PRIMITIVEleft,STRINGright),
						  funid, spec, sigexp, STRING) )
	| OVERLOAD OP_opt vid WITH tyvar COLON ty
			( OVERLOADDec(I(OVERLOADleft,tyright),
				      OP_opt, vid, tyvar, ty) )
	| INSTANCE OP_opt vid WITH longtycon EQUALS longvid
			( INSTANCEDec(I(INSTANCEleft,longvidright),
				      OP_opt, vid, longtycon, longvid) )
	| INSTANCE scon WITH longtycon
			( INSTANCESCONDec(I(INSTANCEleft,longtyconright),
					  scon, longtycon) )
	| INFIX d_opt vid_list1
			( INFIXMULTIDec(I(INFIXleft,vid_list1right),
					d_opt, vid_list1) )
	| INFIXR d_opt vid_list1
			( INFIXRMULTIDec(I(INFIXRleft,vid_list1right),
					 d_opt, vid_list1) )
	| NONFIX vid_list1
			( NONFIXMULTIDec(I(NONFIXleft,vid_list1right),
					 vid_list1) )

     WITHTYPE_typbind_opt:
	  WITHTYPE typbind	( SOME typbind )
	| (*empty*)		( NONE )

     vid_list1:
	  vid vid_list1		( vid::vid_list1 )
	| vid			( vid::[] )

     longstrid_list1:
	  longstrid longstrid_list1	( longstrid::longstrid_list1 )
	| longstrid			( longstrid::[] )

     d_opt:
	  d			( SOME d )
	| (*empty*)		( NONE )



  (* Core: Bindings *)

  valbind:
	  pat EQUALS exp AND_valbind_opt
			( PLAINValBind(I(patleft,AND_valbind_optright),
				       pat, exp, AND_valbind_opt) )
	| REC valbind
			( RECValBind(I(RECleft,valbindright), valbind) )

      AND_valbind_opt:
	  AND valbind	( SOME valbind )
	| (*empty*)	( NONE )


  fvalbind:
	  fmatch AND_fvalbind_opt
			( FvalBind(I(fmatchleft,AND_fvalbind_optright),
				     fmatch, AND_fvalbind_opt) )
     AND_fvalbind_opt:
	  AND fvalbind	( SOME fvalbind )
	| (*empty*)	( NONE )

  fmatch:
	  fmrule BAR_fmatch_opt
			( Fmatch(I(fmruleleft,BAR_fmatch_optright),
				 fmrule, BAR_fmatch_opt) )

     BAR_fmatch_opt:
	  BAR fmatch	( SOME fmatch )
	| (*empty*)	( NONE )

  fmrule:
	  fpat EQUALS exp
			( Fmrule(I(fpatleft,expright), fpat, exp) )

  fpat:
	  pat		( pat )

  typbind:
	  tyvarseq tycon AND_typbind_opt
			( NEWTypBind(I(tyvarseqleft,AND_typbind_optright),
				     tyvarseq, tycon, AND_typbind_opt) )
	| tyvarseq tycon EQUALS ty AND_typbind_opt
			( EQUALTypBind(I(tyvarseqleft,AND_typbind_optright),
				  tyvarseq, tycon, ty, AND_typbind_opt) )

     AND_typbind_opt:
	  AND typbind	( SOME typbind )
	| (*empty*)	( NONE )


  datbind:
	  tyvarseq tycon AND_datbind_opt
			( OPENDatBind(I(tyvarseqleft,AND_datbind_optright),
				      tyvarseq, tycon, AND_datbind_opt) )
	| tyvarseq tycon EQUALS conbind AND_datbind_opt
			( CLOSEDDatBind(I(tyvarseqleft,AND_datbind_optright),
					tyvarseq, tycon, conbind,
					AND_datbind_opt))
     datbind0:
	  tycon AND_datbind_opt
			( OPENDatBind(I(tyconleft,AND_datbind_optright),
				      Seq(I(defaultPos,defaultPos), []),
				      tycon, AND_datbind_opt) )
	| tycon EQUALS conbind AND_datbind_opt
			( CLOSEDDatBind(I(tyconleft,AND_datbind_optright),
					Seq(I(defaultPos,defaultPos), []),
					tycon, conbind, AND_datbind_opt) )
     datbind1:
	  tyvarseq1 tycon AND_datbind_opt
			( OPENDatBind(I(tyvarseq1left,AND_datbind_optright),
				      tyvarseq1, tycon, AND_datbind_opt) )
	| tyvarseq1 tycon EQUALS conbind AND_datbind_opt
			( CLOSEDDatBind(I(tyvarseq1left,AND_datbind_optright),
					tyvarseq1, tycon, conbind,
					AND_datbind_opt) )
     AND_datbind_opt:
	  AND datbind	( SOME datbind )
	| (*empty*)	( NONE )


  conbind:
	  OP_opt vid OF_ty_opt BAR_conbind_opt
			( ConBind(I(OP_optleft,BAR_conbind_optright),
				  OP_opt, vid, OF_ty_opt, BAR_conbind_opt) )

     BAR_conbind_opt:
	  BAR conbind	( SOME conbind )
	| (*empty*)	( NONE )

     OF_ty_opt:
	  OF ty		( SOME ty )
	| (*empty*)	( NONE )


  dconbind:
	  OP_opt vid OF_ty_opt COLON tyvarseq longtycon AND_dconbind_opt
			( NEWDconBind(I(OP_optleft,AND_dconbind_optright),
				      OP_opt, vid, OF_ty_opt, tyvarseq,
				      longtycon, AND_dconbind_opt) )
	| OP_opt vid EQUALS OP_opt longvid AND_dconbind_opt
			( EQUALDconBind(I(OP_opt1left,AND_dconbind_optright),
					OP_opt1, vid, OP_opt2, longvid,
					AND_dconbind_opt) )
     AND_dconbind_opt:
	  AND dconbind	( SOME dconbind )
	| (*empty*)	( NONE )


  exbind:
	  OP_opt vid OF_ty_opt AND_exbind_opt
			( NEWExBind(I(OP_optleft,AND_exbind_optright),
				    OP_opt, vid, OF_ty_opt, AND_exbind_opt) )
	| OP_opt vid EQUALS OP_opt longvid AND_exbind_opt
			( EQUALExBind(I(OP_opt1left,AND_exbind_optright),
				      OP_opt1, vid, OP_opt2, longvid,
				      AND_exbind_opt) )
     AND_exbind_opt:
	  AND exbind	( SOME exbind )
	| (*empty*)	( NONE )



  (* Core: Patterns *)

  atpat:
	  UNDERBAR	( WILDCARDAtPat(I(UNDERBARleft,UNDERBARright)) )
	| longvid'
			( LONGVIDAtPat(I(longvid'left,longvid'right),
				       SANSOp, longvid') )
	| OP longvid	( LONGVIDAtPat(I(OPleft,longvidright),
				       WITHOp, longvid) )
	| scon		( SCONAtPat(I(sconleft,sconright), scon) )
	| LBRACE patrow_opt RBRACE
			( RECORDAtPat(I(LBRACEleft,RBRACEright), patrow_opt) )
	| LPAR RPAR	( UNITAtPat(I(LPARleft,RPARright)) )
	| LPAR pat_COMMA_list2 RPAR
			( TUPLEAtPat(I(LPARleft,RPARright), pat_COMMA_list2) )
	| LBRACK pat_COMMA_list0 RBRACK
			( LISTAtPat(I(LBRACKleft,RBRACKright),
				    pat_COMMA_list0) )
	| HASHBRACK pat_COMMA_list0 RBRACK
			( VECTORAtPat(I(HASHBRACKleft,RBRACKright),
				    pat_COMMA_list0) )
	| LPAR pat_BAR_list2 RPAR
			( ALTAtPat(I(LPARleft,RPARright),
				   pat_BAR_list2) )
	| LPAR pat RPAR ( PARAtPat(I(LPARleft,RPARright), pat) )


     pat_COMMA_list0:
	  pat_COMMA_list1		( pat_COMMA_list1 )
	| (*empty*)			( [] )

     pat_COMMA_list1:
	  pat COMMA pat_COMMA_list1	( pat::pat_COMMA_list1 )
	| pat				( pat::[] )

     pat_COMMA_list2:
	  pat COMMA pat_COMMA_list1	( pat::pat_COMMA_list1 )

     pat_BAR_list2:
	  pat BAR pat			( [pat1,pat2] )
	| pat BAR pat_BAR_list2		( pat::pat_BAR_list2 )


  patrow:
	  DOTS		( WILDCARDPatRow(I(DOTSleft,DOTSright)) )
	| lab EQUALS pat COMMA_patrow_opt
			( ROWPatRow(I(lableft,COMMA_patrow_optright),
				      lab, pat, COMMA_patrow_opt) )
	| vid' COLON_ty_opt AS_pat_opt COMMA_patrow_opt
			( VIDPatRow(I(vid'left,COMMA_patrow_optright),
				    vid', COLON_ty_opt, AS_pat_opt,
				    COMMA_patrow_opt) )
     COMMA_patrow_opt:
	  COMMA patrow	( SOME patrow )
	| (*empty*)	( NONE )

     COLON_ty_opt:
	  COLON ty	( SOME ty )
	| (*empty*)	( NONE )

     AS_pat_opt:
	  AS pat	( SOME pat )
	| (*empty*)	( NONE )

     patrow_opt:
	  patrow	( SOME patrow )
	| (*empty*)	( NONE )



  infpat:
	  atpat		( ATPATPat(I(atpatleft,atpatright), atpat) )
	| infpat atpat	( APPPat(I(infpatleft,atpatright), infpat, atpat) )
	(*OP_opt longvid atpat
			( included above... )*)
	(*pat vid pat	( included above... )*)

  pat:
	  infpat	( infpat )
	| pat COLON ty	( TYPEDPat(I(patleft,tyright), pat, ty) )
	| NON pat	( NONPat(I(NONleft,patright), pat) )
	| pat AS pat	( ASPat(I(pat1left,pat2right), pat1, pat2) )
	| pat WHERE atexp
			( WHENPat(I(patleft,atexpright), pat, atexp) )
	| pat WITHVAL valbind END
			( WITHVALPat(I(patleft,ENDright), pat, valbind) )
	| pat WITHFUN fvalbind END
			( WITHFUNPat(I(patleft,ENDright), pat, fvalbind) )



  (* Core: Types *)

  ty:
	  tupty		 ( tupty )
	| tupty ARROW ty ( ARROWTy(I(tuptyleft,tyright), tupty, ty) )

     tupty:
	  ty_STAR_list	 ( TUPLETy(I(ty_STAR_listleft,ty_STAR_listright),
				   ty_STAR_list) )

     ty_STAR_list:
	  consty STAR ty_STAR_list	( consty::ty_STAR_list )
	| consty	 		( consty::[] )	  

     consty:
	  atty			( atty )
	| tyseq longtycon	( TYCONTy(I(tyseqleft,longtyconright),
					  tyseq, longtycon) )
     atty:
	  tyvar		( TYVARTy(I(tyvarleft,tyvarright), tyvar) )
	| LBRACE tyrow_opt RBRACE
			( RECORDTy(I(LBRACEleft,RBRACEright), tyrow_opt) )
	| LPAR ty RPAR	( PARTy(I(LPARleft,RPARright), ty) )


  tyrow:
	  lab COLON ty COMMA_tyrow_opt
			( ROWTyRow(I(lableft,COMMA_tyrow_optright),
				   lab, ty, COMMA_tyrow_opt) )
     COMMA_tyrow_opt:
	  COMMA tyrow	( SOME tyrow )
	| (*empty*)	( NONE )

     tyrow_opt:
	  tyrow		( SOME tyrow )
	| (*empty*)	( NONE )



  (* Core: Sequences *)

  tyseq:
	  consty			( Seq(I(constyleft,constyright),
					      [consty]) )
	| (*empty*)			( Seq(I(defaultPos,defaultPos), []) )
	| LPAR ty_COMMA_list2 RPAR	( Seq(I(LPARleft,RPARright),
					      ty_COMMA_list2) )
     ty_COMMA_list2:
	  ty COMMA ty_COMMA_list2	( ty::ty_COMMA_list2 )
	| ty COMMA ty			( [ty1, ty2] )


  tyvarseq:
	  tyvarseq1			( tyvarseq1 )
	| (*empty*)			( Seq(I(defaultPos,defaultPos), []) )
     tyvarseq1:
	  tyvar				( Seq(I(tyvarleft,tyvarright), [tyvar]))
	| LPAR tyvar_COMMA_list1 RPAR	( Seq(I(LPARleft,RPARright),
					      tyvar_COMMA_list1) )
     tyvar_COMMA_list1:
	  tyvar COMMA tyvar_COMMA_list1	( tyvar::tyvar_COMMA_list1 )
	| tyvar				( tyvar::[] )




  (* Modules: Structures *)

  strexp:
	  strexp'	( strexp' )
	| strexp COLON sigexp
			( TRANSStrExp(I(strexpleft,sigexpright),
				      strexp, sigexp) )
	| strexp COLONGREATER sigexp
			( OPAQStrExp(I(strexpleft,sigexpright), strexp, sigexp))

     strexp':
	  STRUCT dec END
			( STRUCTStrExp(I(STRUCTleft,ENDright), dec) )
	| longstrid	( LONGSTRIDStrExp(I(longstridleft,longstridright),
					  longstrid) )
	| longfunid LPAR strexp RPAR
			( APPStrExp(I(longfunidleft,RPARright),
				    longfunid, strexp) )
	| longfunid LPAR dec RPAR
			( APPDECStrExp(I(longfunidleft,RPARright),
				       longfunid, dec) )
	| LPAR strexp RPAR
			( PARStrExp(I(LPARleft,RPARright), strexp) )
	| LET dec IN strexp END
			( LETStrExp(I(LETleft,ENDright), dec, strexp) )


  strbind:
	  strid COLON_sigexp_opt EQUALS strexp__AND_strbind_opt
			( TRANSStrBind(I(stridleft,
					 strexp__AND_strbind_optright),
				       strid, COLON_sigexp_opt,
				       #1 strexp__AND_strbind_opt,
				       #2 strexp__AND_strbind_opt) )
	| strid COLONGREATER sigexp EQUALS strexp__AND_strbind_opt
			( OPAQStrBind(I(stridleft,strexp__AND_strbind_optright),
				      strid, sigexp, #1 strexp__AND_strbind_opt,
				      #2 strexp__AND_strbind_opt) )
     AND_strbind_opt:
	  AND strbind	( SOME strbind )
	| (*empty*)	( NONE )

     strexp__AND_strbind_opt:
	  strexp' AND_strbind_opt
			( ( strexp', AND_strbind_opt ) )
	| strexp COLON sigexp__AND_strbind_opt
			( ( TRANSStrExp(I(strexpleft,
					  sigexp__AND_strbind_optright),
					strexp, #1 sigexp__AND_strbind_opt),
			    #2 sigexp__AND_strbind_opt ) )
	| strexp COLONGREATER sigexp__AND_strbind_opt
			( ( OPAQStrExp(I(strexpleft,
					 sigexp__AND_strbind_optright),
				       strexp, #1 sigexp__AND_strbind_opt),
			    #2 sigexp__AND_strbind_opt ) )

     sigexp__AND_strbind_opt:
	  sigexp' AND_strbind_opt
			( ( sigexp', AND_strbind_opt ) )
	| sigexp WHERE tyreadesc__AND_strbind_opt
			( ( WHERETYPESigExp(I(sigexpleft,
					      tyreadesc__AND_strbind_optright),
					   sigexp,
					   #1 tyreadesc__AND_strbind_opt),
			    #2 tyreadesc__AND_strbind_opt ) )

     tyreadesc__AND_strbind_opt:
	  TYPE tyvarseq longtycon EQUALS ty AND_tyreadesc_opt__AND_strbind_opt
			( ( TyReaDesc(I(TYPEleft,
				       AND_tyreadesc_opt__AND_strbind_optright),
				      tyvarseq, longtycon, ty,
				      #1 AND_tyreadesc_opt__AND_strbind_opt),
			    #2 AND_tyreadesc_opt__AND_strbind_opt ) )

     AND_tyreadesc_opt__AND_strbind_opt:
	  AND_strbind_opt	( ( NONE, AND_strbind_opt ) )
	| AND tyreadesc__AND_strbind_opt
	  			( ( SOME(#1 tyreadesc__AND_strbind_opt),
				    #2 tyreadesc__AND_strbind_opt ) )


     COLON_sigexp_opt:
	  COLON sigexp	( SOME sigexp )
	| (*empty*)	( NONE )



  (* Modules: Signatures *)

  sigexp:
	  sigexp'	( sigexp' )
	| sigexp WHERE tyreadesc
			( WHERETYPESigExp(I(sigexpleft,tyreadescright),
					  sigexp, tyreadesc) )
     sigexp':
	  SIG spec END	( SIGSigExp(I(SIGleft,ENDright), spec) )
	| longsigid	( LONGSIGIDSigExp(I(longsigidleft,longsigidright),
					  longsigid) )
	| LPAR sigexp RPAR
			( PARSigExp(I(LPARleft,RPARright), sigexp) )
	| sigexp WHERE longstrid EQUALS longstrid
			( WHERESTRUCTURESigExp(I(sigexpleft,longstrid2right),
					       sigexp, longstrid1, longstrid2) )

  sigbind:
	  sigid EQUALS sigexp__AND_sigbind_opt
			( SigBind(I(sigidleft,sigexp__AND_sigbind_optright),
				  sigid, #1 sigexp__AND_sigbind_opt,
				  #2 sigexp__AND_sigbind_opt) )

     AND_sigbind_opt:
	  AND sigbind	( SOME sigbind )
	| (*empty*)	( NONE )

     sigexp__AND_sigbind_opt:
	  sigexp' AND_sigbind_opt
	  		( ( sigexp', AND_sigbind_opt ) )
	| sigexp WHERE tyreadesc__AND_sigbind_opt
			( ( WHERETYPESigExp(I(sigexpleft,
					      tyreadesc__AND_sigbind_optright),
					   sigexp,
					   #1 tyreadesc__AND_sigbind_opt),
			    #2 tyreadesc__AND_sigbind_opt ) )

     tyreadesc__AND_sigbind_opt:
	  TYPE tyvarseq longtycon EQUALS ty AND_tyreadesc_opt__AND_sigbind_opt
			( ( TyReaDesc(I(TYPEleft,
				       AND_tyreadesc_opt__AND_sigbind_optright),
				      tyvarseq, longtycon, ty,
				      #1 AND_tyreadesc_opt__AND_sigbind_opt),
			    #2 AND_tyreadesc_opt__AND_sigbind_opt ) )

     AND_tyreadesc_opt__AND_sigbind_opt:
	  AND_sigbind_opt	( ( NONE, AND_sigbind_opt) )
	| AND tyreadesc__AND_sigbind_opt
	  			( ( SOME(#1 tyreadesc__AND_sigbind_opt),
				    #2 tyreadesc__AND_sigbind_opt ) )


  tyreadesc:
	  TYPE tyvarseq longtycon EQUALS ty AND_tyreadesc_opt
			( TyReaDesc(I(TYPEleft,AND_tyreadesc_optright),
				    tyvarseq, longtycon, ty,
				    AND_tyreadesc_opt) )
     AND_tyreadesc_opt:
	  AND tyreadesc	( SOME tyreadesc )
	| (*empty*)	( NONE )



  (* Modules: Functors *)

  funbind:
	  funid LPAR strid COLON sigexp RPAR COLON_sigexp_opt EQUALS
		strexp__AND_funbind_opt
			( TRANSFunBind(I(funidleft,
					 strexp__AND_funbind_optright),
				       funid, strid, sigexp, COLON_sigexp_opt,
				       #1 strexp__AND_funbind_opt,
				       #2 strexp__AND_funbind_opt) )
	| funid LPAR strid COLON sigexp RPAR COLONGREATER sigexp EQUALS
		strexp__AND_funbind_opt
			( OPAQFunBind(I(funidleft,strexp__AND_funbind_optright),
				      funid, strid, sigexp1, sigexp2,
				      #1 strexp__AND_funbind_opt,
				      #2 strexp__AND_funbind_opt) )
	| funid LPAR spec RPAR COLON_sigexp_opt EQUALS strexp__AND_funbind_opt
			( TRANSSPECFunBind(I(funidleft,
					     strexp__AND_funbind_optright),
					   funid, spec, COLON_sigexp_opt,
					   #1 strexp__AND_funbind_opt,
					   #2 strexp__AND_funbind_opt) )
	| funid LPAR spec RPAR COLONGREATER sigexp EQUALS
		strexp__AND_funbind_opt
			( OPAQSPECFunBind(I(funidleft,
					    strexp__AND_funbind_optright),
					  funid, spec, sigexp,
					  #1 strexp__AND_funbind_opt,
					  #2 strexp__AND_funbind_opt) )
     AND_funbind_opt:
	  AND funbind	( SOME funbind )
	| (*empty*)	( NONE )

     strexp__AND_funbind_opt:
	  strexp' AND_funbind_opt
			( ( strexp', AND_funbind_opt ) )
	| strexp COLON sigexp__AND_funbind_opt
			( ( TRANSStrExp(I(strexpleft,
					  sigexp__AND_funbind_optright),
					strexp, #1 sigexp__AND_funbind_opt),
			    #2 sigexp__AND_funbind_opt ) )
	| strexp COLONGREATER sigexp__AND_funbind_opt
			( ( OPAQStrExp(I(strexpleft,
					 sigexp__AND_funbind_optright),
				       strexp, #1 sigexp__AND_funbind_opt),
			    #2 sigexp__AND_funbind_opt ) )

     sigexp__AND_funbind_opt:
	  sigexp' AND_funbind_opt
			( ( sigexp', AND_funbind_opt ) )
	| sigexp WHERE tyreadesc__AND_funbind_opt
			( ( WHERETYPESigExp(I(sigexpleft,
					      tyreadesc__AND_funbind_optright),
					   sigexp,
					   #1 tyreadesc__AND_funbind_opt),
			    #2 tyreadesc__AND_funbind_opt ) )

     tyreadesc__AND_funbind_opt:
	  TYPE tyvarseq longtycon EQUALS ty AND_tyreadesc_opt__AND_funbind_opt
			( ( TyReaDesc(I(TYPEleft,
				       AND_tyreadesc_opt__AND_funbind_optright),
				      tyvarseq, longtycon, ty,
				      #1 AND_tyreadesc_opt__AND_funbind_opt),
			    #2 AND_tyreadesc_opt__AND_funbind_opt ) )

     AND_tyreadesc_opt__AND_funbind_opt:
	  AND_funbind_opt
			( ( NONE, AND_funbind_opt ) )
	| AND tyreadesc__AND_funbind_opt
	  		( ( SOME(#1 tyreadesc__AND_funbind_opt),
			    #2 tyreadesc__AND_funbind_opt ) )



  (* Modules: Specifications *)

  spec:
	  spec1		( spec1 )
	| (*empty*)	( EMPTYSpec(I(defaultPos,defaultPos)) )

     spec1:
	  spec1'	( spec1' )
	| spec1 spec1' %prec SEMICOLON
			( SEQSpec(I(spec1left,spec1'right), spec1, spec1') )
	| SEMICOLON	( EMPTYSpec(I(SEMICOLONleft,SEMICOLONleft)) )
	| SHARING TYPE longtycon_EQUALS_list2
			( SHARINGTYPESpec(I(SHARINGleft,
					    longtycon_EQUALS_list2right),
					  EMPTYSpec(I(SHARINGleft,SHARINGleft)),
					  longtycon_EQUALS_list2) )
	| spec1 SHARING TYPE longtycon_EQUALS_list2
			( SHARINGTYPESpec(I(spec1left,
					    longtycon_EQUALS_list2right),
					  spec1, longtycon_EQUALS_list2) )
	| SHARING SIGNATURE longsigid_EQUALS_list2
			( SHARINGSIGNATURESpec(I(SHARINGleft,
						 longsigid_EQUALS_list2right),
					       EMPTYSpec(I(SHARINGleft,
							   SHARINGleft)),
					       longsigid_EQUALS_list2) )
	| spec1 SHARING SIGNATURE longsigid_EQUALS_list2
			( SHARINGSIGNATURESpec(I(spec1left,
						 longsigid_EQUALS_list2right),
					       spec1, longsigid_EQUALS_list2) )
	| SHARING longstrid_EQUALS_list2
			( SHARINGSpec(I(SHARINGleft,
					longstrid_EQUALS_list2right),
				      EMPTYSpec(I(SHARINGleft,SHARINGleft)),
				      longstrid_EQUALS_list2) )
	| spec1 SHARING longstrid_EQUALS_list2
			( SHARINGSpec(I(spec1left,longstrid_EQUALS_list2right),
				      spec1, longstrid_EQUALS_list2) )

     spec1':
	  VAL valdesc	( VALSpec(I(VALleft,valdescright), valdesc) )
	| FUN valdesc	( FUNSpec(I(FUNleft,valdescright), valdesc) )
	| TYPE typdesc	( TYPESpec(I(TYPEleft,typdescright), typdesc) )
	| EQTYPE typdesc
	  		( EQTYPESpec(I(EQTYPEleft,typdescright), typdesc) )
	| EQEQTYPE typdesc
	  		( EQEQTYPESpec(I(EQEQTYPEleft,typdescright), typdesc) )
	| DATATYPE datdesc0 WITHTYPE_typdesc_opt
			( DATATYPESpec(I(DATATYPEleft,
					 WITHTYPE_typdesc_optright),
				       datdesc0, WITHTYPE_typdesc_opt) )
	| DATATYPE datdesc1 WITHTYPE_typdesc_opt
			( DATATYPESpec(I(DATATYPEleft,
					 WITHTYPE_typdesc_optright),
				       datdesc1, WITHTYPE_typdesc_opt) )
	| DATATYPE tycon EQUALS DATATYPE longtycon
			( REPLICATIONSpec(I(DATATYPEleft,longtyconright),
					  tycon, longtycon) )
	| CONSTRUCTOR dcondesc
			( CONSTRUCTORSpec(I(CONSTRUCTORleft,dcondescright),
					  dcondesc) )
	| EXCEPTION exdesc
			( EXCEPTIONSpec(I(EXCEPTIONleft,exdescright), exdesc) )
	| STRUCTURE strdesc
			( STRUCTURESpec(I(STRUCTUREleft,strdescright), strdesc))
	| SIGNATURE sigdesc
			( SIGNATURESpec(I(SIGNATUREleft,sigdescright), sigdesc))
	| FUNCTOR fundesc
			( FUNCTORSpec(I(FUNCTORleft,fundescright), fundesc))
	| INCLUDE sigexp
			( INCLUDESpec(I(INCLUDEleft,sigexpright), sigexp) )
	| INCLUDE longsigid_list2
			( INCLUDEMULTISpec(I(INCLUDEleft,longsigid_list2right),
					   longsigid_list2) )
	| PREBOUND strid
			( PREBOUNDSpec(I(PREBOUNDleft,stridright), strid) )
	| OVERLOAD OP_opt vid WITH tyvar COLON ty
			( OVERLOADSpec(I(OVERLOADleft,tyright),
				       OP_opt, vid, tyvar, ty))
	| INSTANCE OP_opt vid WITH longtycon EQUALS longvid
			( INSTANCESpec(I(INSTANCEleft,longvidright),
				       OP_opt, vid, longtycon, longvid) )
	| INSTANCE scon WITH longtycon
			( INSTANCESCONSpec(I(INSTANCEleft,longtyconright),
					   scon, longtycon) )
	| INFIX d_opt vid_list1
			( INFIXMULTISpec(I(INFIXleft,vid_list1right), d_opt,
					 vid_list1) )
	| INFIXR d_opt vid_list1
			( INFIXRMULTISpec(I(INFIXRleft,vid_list1right), d_opt,
					  vid_list1) )
	| NONFIX vid_list1
			( NONFIXMULTISpec(I(NONFIXleft,vid_list1right),
					  vid_list1) )

     WITHTYPE_typdesc_opt:
	  WITHTYPE typdesc	( SOME typdesc )
	| (*empty*)		( NONE )

     longsigid_list2:
	  longsigid longsigid_list2	( longsigid::longsigid_list2 )
	| longsigid longsigid		( longsigid1::longsigid2::[] )

     longtycon_EQUALS_list1:
	  longtycon EQUALS longtycon_EQUALS_list1
				( longtycon::longtycon_EQUALS_list1 )
	| longtycon		( longtycon::[] )

     longtycon_EQUALS_list2:
	  longtycon EQUALS longtycon_EQUALS_list1
				( longtycon::longtycon_EQUALS_list1 )
     longsigid_EQUALS_list1:
	  longsigid EQUALS longsigid_EQUALS_list1
				( longsigid::longsigid_EQUALS_list1 )
	| longsigid		( longsigid::[] )

     longsigid_EQUALS_list2:
	  longsigid EQUALS longsigid_EQUALS_list1
				( longsigid::longsigid_EQUALS_list1 )
     longstrid_EQUALS_list1:
	  longstrid EQUALS longstrid_EQUALS_list1
				( longstrid::longstrid_EQUALS_list1 )
	| longstrid		( longstrid::[] )

     longstrid_EQUALS_list2:
	  longstrid EQUALS longstrid_EQUALS_list1
				( longstrid::longstrid_EQUALS_list1 )


  (* Modules: Descriptions *)

  valdesc:
	  OP_opt vid COLON ty AND_valdesc_opt
	  		( ValDesc(I(OP_optleft,AND_valdesc_optright),
				  OP_opt, vid, ty, AND_valdesc_opt) )
     AND_valdesc_opt:
	  AND valdesc	( SOME valdesc )
	| (*empty*)	( NONE )

  typdesc:
	  tyvarseq tycon AND_typdesc_opt
			( NEWTypDesc(I(tyvarseqleft,AND_typdesc_optright),
				     tyvarseq, tycon, AND_typdesc_opt) )
	| tyvarseq tycon EQUALS ty AND_typdesc_opt
			( EQUALTypDesc(I(tyvarseqleft,AND_typdesc_optright),
				       tyvarseq, tycon, ty, AND_typdesc_opt) )
     AND_typdesc_opt:
	  AND typdesc	( SOME typdesc )
	| (*empty*)	( NONE )

  datdesc:
	  tyvarseq tycon AND_datdesc_opt
			( OPENDatDesc(I(tyvarseqleft,AND_datdesc_optright),
				      tyvarseq, tycon, AND_datdesc_opt) )
	| tyvarseq tycon EQUALS condesc AND_datdesc_opt
			( CLOSEDDatDesc(I(tyvarseqleft,AND_datdesc_optright),
				     tyvarseq, tycon, condesc, AND_datdesc_opt))
     datdesc0:
	  tycon AND_datdesc_opt
			( OPENDatDesc(I(tyconleft,AND_datdesc_optright),
				      Seq(I(defaultPos,defaultPos), []),
				      tycon, AND_datdesc_opt) )
	| tycon EQUALS condesc AND_datdesc_opt
	  		( CLOSEDDatDesc(I(tyconleft,AND_datdesc_optright),
	  				Seq(I(defaultPos,defaultPos), []),
					tycon, condesc, AND_datdesc_opt) )
     datdesc1:
	  tyvarseq1 tycon AND_datdesc_opt
			( OPENDatDesc(I(tyvarseq1left,AND_datdesc_optright),
				      tyvarseq1, tycon, AND_datdesc_opt) )
	| tyvarseq1 tycon EQUALS condesc AND_datdesc_opt
	  		( CLOSEDDatDesc(I(tyvarseq1left,AND_datdesc_optright),
	  				tyvarseq1, tycon, condesc,
					AND_datdesc_opt) )
     AND_datdesc_opt:
	  AND datdesc	( SOME datdesc )
	| (*empty*)	( NONE )

  condesc:
	  OP_opt vid OF_ty_opt BAR_condesc_opt
			( ConDesc(I(OP_optleft,BAR_condesc_optright),
				  OP_opt, vid, OF_ty_opt, BAR_condesc_opt) )
     BAR_condesc_opt:
	  BAR condesc	( SOME condesc )
	| (*empty*)	( NONE )

  dcondesc:
	  OP_opt vid OF_ty_opt COLON tyvarseq longtycon AND_dcondesc_opt
			( NEWDconDesc(I(OP_optleft,AND_dcondesc_optright),
				      OP_opt, vid, OF_ty_opt,
				      tyvarseq, longtycon, AND_dcondesc_opt) )
	| OP_opt vid EQUALS OP_opt longvid AND_dcondesc_opt
			( EQUALDconDesc(I(OP_opt1left,AND_dcondesc_optright),
					OP_opt1, vid, OP_opt2, longvid,
					AND_dcondesc_opt) )
     AND_dcondesc_opt:
	  AND dcondesc	( SOME dcondesc )
	| (*empty*)	( NONE )


  exdesc:
	  OP_opt vid OF_ty_opt AND_exdesc_opt
	  		( NEWExDesc(I(OP_optleft,AND_exdesc_optright),
	  			    OP_opt, vid, OF_ty_opt, AND_exdesc_opt) )
	| OP_opt vid EQUALS OP_opt longvid AND_exdesc_opt
			( EQUALExDesc(I(OP_opt1left,AND_exdesc_optright),
				      OP_opt1, vid, OP_opt2, longvid,
				      AND_exdesc_opt) )
     AND_exdesc_opt:
	  AND exdesc	( SOME exdesc )
	| (*empty*)	( NONE )

  strdesc:
	  strid COLON sigexp__AND_strdesc_opt
			( NEWStrDesc(I(stridleft,sigexp__AND_strdesc_optright),
				     strid, #1 sigexp__AND_strdesc_opt,
				     #2 sigexp__AND_strdesc_opt) )
	| strid COLON_sigexp_opt EQUALS longstrid AND_strdesc_opt
	  		( EQUALStrDesc(I(stridleft,AND_strdesc_optright),
				       strid, COLON_sigexp_opt, longstrid,
				       AND_strdesc_opt) )
     AND_strdesc_opt:
	  AND strdesc	( SOME strdesc )
	| (*empty*)	( NONE )

     sigexp__AND_strdesc_opt:
	  sigexp' AND_strdesc_opt
			( ( sigexp', AND_strdesc_opt ) )
	| sigexp WHERE tyreadesc__AND_strdesc_opt
			( ( WHERETYPESigExp(I(sigexpleft,
					      tyreadesc__AND_strdesc_optright),
					   sigexp,
					   #1 tyreadesc__AND_strdesc_opt),
			    #2 tyreadesc__AND_strdesc_opt ) )

     tyreadesc__AND_strdesc_opt:
	  TYPE tyvarseq longtycon EQUALS ty AND_tyreadesc_opt__AND_strdesc_opt
			( ( TyReaDesc(I(TYPEleft,
				       AND_tyreadesc_opt__AND_strdesc_optright),
				      tyvarseq, longtycon, ty,
				      #1 AND_tyreadesc_opt__AND_strdesc_opt),
			    #2 AND_tyreadesc_opt__AND_strdesc_opt ) )

     AND_tyreadesc_opt__AND_strdesc_opt:
	  AND_strdesc_opt	( ( NONE, AND_strdesc_opt ) )
	| AND tyreadesc__AND_strdesc_opt
	  			( ( SOME(#1 tyreadesc__AND_strdesc_opt),
				    #2 tyreadesc__AND_strdesc_opt ) )

  sigdesc:
	  sigid AND_sigdesc_opt
			( NEWSigDesc(I(sigidleft,AND_sigdesc_optright),
				     sigid, AND_sigdesc_opt) )
	| sigid EQUALS sigexp__AND_sigdesc_opt
			( EQUALSigDesc(I(sigidleft,
					 sigexp__AND_sigdesc_optright),
				       sigid, #1 sigexp__AND_sigdesc_opt,
				       #2 sigexp__AND_sigdesc_opt) )

     AND_sigdesc_opt:
	  AND sigdesc	( SOME sigdesc )
	| (*empty*)	( NONE )

     sigexp__AND_sigdesc_opt:
	  sigexp' AND_sigdesc_opt
	  		( ( sigexp', AND_sigdesc_opt ) )
	| sigexp WHERE tyreadesc__AND_sigdesc_opt
			( ( WHERETYPESigExp(I(sigexpleft,
					      tyreadesc__AND_sigdesc_optright),
					   sigexp,
					   #1 tyreadesc__AND_sigdesc_opt),
			    #2 tyreadesc__AND_sigdesc_opt ) )

     tyreadesc__AND_sigdesc_opt:
	  TYPE tyvarseq longtycon EQUALS ty AND_tyreadesc_opt__AND_sigdesc_opt
			( ( TyReaDesc(I(TYPEleft,
				       AND_tyreadesc_opt__AND_sigdesc_optright),
				      tyvarseq, longtycon, ty,
				      #1 AND_tyreadesc_opt__AND_sigdesc_opt),
			    #2 AND_tyreadesc_opt__AND_sigdesc_opt ) )

     AND_tyreadesc_opt__AND_sigdesc_opt:
	  AND_sigdesc_opt	( ( NONE, AND_sigdesc_opt) )
	| AND tyreadesc__AND_sigdesc_opt
	  			( ( SOME(#1 tyreadesc__AND_sigdesc_opt),
				    #2 tyreadesc__AND_sigdesc_opt ) )

  fundesc:
	  funid LPAR strid COLON sigexp RPAR COLON sigexp__AND_fundesc_opt
			( FunDesc(I(funidleft,sigexp__AND_fundesc_optright),
				  funid, strid, sigexp,
				  #1 sigexp__AND_fundesc_opt,
				  #2 sigexp__AND_fundesc_opt) )
	| funid LPAR spec RPAR COLON sigexp__AND_fundesc_opt
			( SPECFunDesc(I(funidleft,
					sigexp__AND_fundesc_optright),
				      funid, spec, #1 sigexp__AND_fundesc_opt,
				      #2 sigexp__AND_fundesc_opt) )
     AND_fundesc_opt:
	  AND fundesc	( SOME fundesc )
	| (*empty*)	( NONE )

     sigexp__AND_fundesc_opt:
	  sigexp' AND_fundesc_opt
			( ( sigexp', AND_fundesc_opt ) )
	| sigexp WHERE tyreadesc__AND_fundesc_opt
			( ( WHERETYPESigExp(I(sigexpleft,
					      tyreadesc__AND_fundesc_optright),
					   sigexp,
					   #1 tyreadesc__AND_fundesc_opt),
			    #2 tyreadesc__AND_fundesc_opt ) )

     tyreadesc__AND_fundesc_opt:
	  TYPE tyvarseq longtycon EQUALS ty AND_tyreadesc_opt__AND_fundesc_opt
			( ( TyReaDesc(I(TYPEleft,
				       AND_tyreadesc_opt__AND_fundesc_optright),
				      tyvarseq, longtycon, ty,
				      #1 AND_tyreadesc_opt__AND_fundesc_opt),
			    #2 AND_tyreadesc_opt__AND_fundesc_opt ) )

     AND_tyreadesc_opt__AND_fundesc_opt:
	  AND_fundesc_opt
			( ( NONE, AND_fundesc_opt ) )
	| AND tyreadesc__AND_fundesc_opt
	  		( ( SOME(#1 tyreadesc__AND_fundesc_opt),
			    #2 tyreadesc__AND_fundesc_opt ) )



  (* Programs *)

  program:
	  dec'
	  		( DECProgram(I(dec'left,dec'right),
				     dec', NONE) )
	| dec' SEMICOLON program_opt'
	  		( DECProgram(I(dec'left,program_opt'right),
				     dec', program_opt') )
	| exp SEMICOLON program_opt'
	  		( EXPProgram(I(expleft,program_opt'right),
				     exp, program_opt') )

     program_opt:
	  program		( SOME program )
	| (*empty*)		( NONE )

     program_opt':
	  program_opt		( program_opt )
	| SEMICOLON program_opt'( program_opt' )



  (* Components *)

  component:
	  import0 program_opt	( Component(I(import0left,program_optright),
					    import0, program_opt) )

  import0:
	  import1		( import1 )
	| (*empty*)		( EMPTYImport(I(defaultPos,defaultPos)) )

     import1:
	  IMPORT spec FROM STRING
	  			( IMPORTImport(I(IMPORTleft,STRINGright),
					       spec, STRING) )
	| import1 import1 %prec SEMICOLON
				( SEQImport(I(import11left,import12right),
					    import11, import12) )
	| SEMICOLON		( EMPTYImport(I(SEMICOLONleft,SEMICOLONleft)) )
